ZVSE
!#UN:P5/0;
!#IF:V100/1; enable Video
!#VRz199:S^..\Data\Zvs\Cmp.res^;
!#VRz198:S^Z11p^;
*** 2 next map
!#VRv701:S0; quest 1
!#VRv702:S0; quest 2
!#VRv703:S0; quest 3
!#VRv704:S0; quest 4
!#VRv705:S0; quest 5
!#VRv706:S0; quest 6
!#VRv707:S0; quest 7
*** #1 Name and portrait
!#VRz100:S^ Пора начинать настоящую игру. Вы готовы, Герой?
(рекомендуем сперва пройти обучающую игру).^;
!#VRz101:S^ Ваше имя:^;
!#VRz102:S^ Выберите ваш портрет, герой:^;
!#VRz103:S^%Z199\11_901.bmp^;
!#VRz104:S^%Z199\11_902.bmp^;
!#VRz105:S^%Z199\11_903.bmp^;
!#VRz106:S^%Z199\11_904.bmp^;
!#VRz107:S^Первый^;
!#VRz108:S^Второй^;
!#VRz109:S^Третий^;
!#VRz110:S^Четвертый^;
!#IF:D1/z100/z101/z102/z103/z104/z105/z106/z107/z108/z109/z110/z107/z108/z109/z110;
!#IF:F1/z107/z108/z109/z110/0;

!#IF:E100/1; Get Name dialoge
!#VRz1:H1; !#VRz1&-1:S^Ваня^;
!#VRz400:Sz1; Hero's name in z400
!#VRz396:S^Это вы, Герой^;
!#VRz397:S^%Z199\11_901.bmp^;
!#VRz395:S^%Z199\11_905.bmp^;
!#VRz398:S^%Z198_1l.pcx^;
!#VRz399:S^%Z198_1s.pcx^;
!#VRz397&v100=2:S^%Z199\11_902.bmp^;
!#VRz395&v100=2:S^%Z199\11_906.bmp^;
!#VRz398&v100=2:S^%Z198_2l.pcx^;
!#VRz399&v100=2:S^%Z198_2s.pcx^;
!#VRz397&v100=3:S^%Z199\11_903.bmp^;
!#VRz395&v100=3:S^%Z199\11_907.bmp^;
!#VRz398&v100=3:S^%Z198_3l.pcx^;
!#VRz399&v100=3:S^%Z198_3s.pcx^;
!#VRz397&v100=4:S^%Z199\11_904.bmp^;
!#VRz395&v100=4:S^%Z199\11_908.bmp^;
!#VRz398&v100=4:S^%Z198_4l.pcx^;
!#VRz399&v100=4:S^%Z198_4s.pcx^;

*** Now all string variables initialization
*** To localize the map change all of them
!#VRz402:S^ Ваш дом, %Z400^;
!#VRz403:S^ Дом вашего друга Дэна^;
!#VRz404:S^ Дом упрямого крестьянина
... Ну, так оно было.^;
!#VRz405:S^^;
!#VRz406:S^ Водяная мельница^;
!#VRz407:S^ Бессмысленный объект Героев 4^;
!#VRz408:S^ Xрабрый рыцарь^; Name of Edrik
!#VRz409:S^ Монах^; Name of Monk
!#VRz410:S^ Точное изображение вашего отца, %Z400^; Name of Statue
!#VRz411:S^ Ваш лучший друг Дэн^;
!#VRz412:S^ Еще одна неделя прошла^;
!#VRz420:S^
Ваш отец был храбрым человеком.
Он решил сам строить свое будущее.
В конце у него не было врагов.
Он был легендарным...

...крестьянином.
"Сынок, подойди и спроси меня,
если не знаешь, что предпринять."
Его слова врезались в вашу память.
^;
!#VRz421:S^ Ты читать умеешь, приятель?
Посмотри на эти большие буквы:
"ОБЪЕКТ ГЕРОЕВ IV"
Тебе придется немного подождать (а может и много :-)^;
!#VRz422:S^ Эй, что мы тебе говорили!
Возвращайся к работе!
А не то мы придем и научим
тебя почтительности!^;
!#VRz423:S^ Мы тебя поймаем!^;
!#VRz424:S^%Z400! Где ты?
Я не могу тебя найти!^;
!#VRz425:S^ Xа! Вот теперь ты действительно нас рассердил!
Мы идем!^;
!#VRz426:S^ Упс.... они уже здесь.^;
!#VRz427:S^ Иди вперед!^;
!#VRz428:S^ Эй, %Z400, не оставляй меня, пожалуйста!
Не уходи!!
Нет!!!
...
Мне надо вернуться... Я не могу остаться здесь...
Они идут...^;
!#VRz429:S^ Да, да, сэр.
Я буду работать изо всех сил!
Я сделаю все, что смогу!
Не волнуйтесь!^;
!#VRz430:S^ Эй, ты, возвращайся к работе!^;
!#VRz431:S^ Что мы тебе сказали?
Вычисти эти уши! ВОЗВРАЩАЙСЯ К РАБОТЕ!
Ты хочешь бросить своих родителей, да?
Xа! После того, как мы обчистим твой дом, мы его сожжем!^;
!#VRz432:S^ Что ж. Вот оно.^;
!#VRz433:S^ Вы стучите в дверь и не получаете ответа.
Какое странное место.
Очевидно, оно давным давно было покинуто.^;
!#VRz434:S^ Дом Кирра^;
!#VRz435:S^ А что вы думали здесь найти?^;
!#VRz436:S^
Вы обыскиваете каждый уголок покинутого дома и находите записку:
"Скорее всего, ты найдешь эту записку, %Z400!
Ты не первый человек, искавший счастья.
Возьми эту книгу ищи в ней помощь."
**Кирр**
Записка приклеена к старой книге.
Может, просто библия...
^;
!#VRz437:S^Ваш отец был умным человеком.
Вы помните, как он говорил:
"Сын, не води дружбы с нытиками.
Старайся избавляться от них.
Но... помни, что избавляться от них нужно
в нужное ВРЕМЯ и в нужном МЕСТЕ.
И попытайся как можно скорее попасть в УБЕЖИЩЕ,
чтобы Крестоносцы не могли тебя найти."
^;
!#VRz438:S^
"Сын, как ты мог быть таким глупым!
Избавься от этого кинжала!
Оставь его в 21*26.
И спрячься в 17*31."
^;

*** CMD Dialogues initialization
*** #2 Ratibors questions
!#VRz210:S^ Ратибор выглядит очень раздражительным.
Похоже, что говорить с ним небезопасно.
И у него явно нет чувства юмора.
Но если вы не станете с ним разговаривать, это вас ни к чему не приведет.^;
!#VRz211:S^ Так что вы отваживаетесь сказать:^;
!#VRz212:S^%Z199\11_009.bmp^;
!#VRz213:S^%Z199\11_010.bmp^;
!#VRz214:S^%Z199\11_011.bmp^;
!#VRz215:S^%Z199\11_012.bmp^;
!#VRz216:S^ Почему бы вам не попросить монахов о духовном руководстве!^;
!#VRz217:S^ Почему бы вам не попросить Крестоносцев помочь вам?^;
!#VRz218:S^ Почему бы вам не отдохнуть в доме Кирра, старик?^;
!#VRz219:S^ Почему бы вам не попросить Дэна научить вас драться вилами? Это гораздо удобнее тяжелого меча.^;
!#VRz220:S^ Монахи^;
!#VRz221:S^ Крестоносцы^;
!#VRz222:S^ Дом Кирра^;
!#VRz223:S^ Вилы Дэна^;
*** #8 Magic book
!#VRz200:S^
У меня есть четыре предмета, которые могут заинтересовать вас.
У каждого из них есть уникальные свойства.
Но у меня нет времени на долгие объяснения.
Так что вам придется самому сделать выбор.
^;
!#VRz201:S^ Какой предмет вы предпочитаете?^;
!#VRz202:S^%Z199\11_005.bmp^;
!#VRz203:S^%Z199\11_006.bmp^;
!#VRz204:S^%Z199\11_007.bmp^;
!#VRz205:S^%Z199\11_008.bmp^;
!#VRz206:S^ Пылающая книга^;
!#VRz207:S^ Земляная книга^;
!#VRz208:S^ Тонкая книга^;
!#VRz209:S^ Влажная книга^;

Logical state in v500
!#VRv500:S0;
Logical steps in flags 500...
500-Guards visited
501-Abandoned building
502-Monk
503-water wheel
504-Ratibor
505-Out
!#IF:V500/0V501/0V502/0V503/0V504/0V505/0;
!#VRv550:S0; Dan's whine state

!#UN:K1/1; no week of ...
!#UN:M1; no month of ...
!#UN:K2/412; set every week of message

!?GE1; First changes
!!HE0:L5/z398/z399; set hero's portrait
!!HE0:B0/z400;
!!HE0:A-3;
!!UN:R1;
!!VRz2:S^^;
!!VRz1:S^
Теплая летняя ночь...
запах свежей травы и дождя наполняет воздух...
Тихие звуки раздаются в роще,
сливаясь и меняя друг друга,
наполняя тьму тайнами...
Легкий ветерок колышит ветви кленов.
Две звезды скатываются с ночного неба
и падают на мягкую шелковистую траву, словно роса.
^;
!!VRz3:S^%Z199\11_015.bmp^;
!!VRz4:S^%Z199\11_014.bmp^;
!!IF:D99/z1/z2/z2/z3/z4/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99; Start up message
! !VRz3:S^%Z199\11_013.bmp^;
! !IFB100/z3/0;
! !IF:P100;

!!VRz1:S^
Тишина делается все глубже,
а потом плач двух новорожденных раздается в ночи.
И старая ведьма предсказывает...
Один пойдет по долгой и извилистой дороге.
Другого ждет судьба воина.
^;
!!VRz3:S^%Z199\11_015.bmp^;
!!IF:D99/z1/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99; Start up message
!!VRz1:S^
Прошли годы. Вы росли вместе.
Но со временем ваша душа становилась все легче
и стремилась к звездам, к летним грозам, к далекому небу.
Дэн ваш друг. Вы провели вместе почти всю жизнь.
Учились сражаться, охотились... но что-то тянуло вас к неизведанному.
Вы мечтали о рыцаре на белом коне,
в сияющих доспехах, отражающих лучи солнца.
Может ли это быть реальностью?
^;
!!IF:D99/z1/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99; Start up message
!!VRz1:S^
Как же его звали... Гелу???
Вы не можете вспомнить...
Неужели это произошло, когда вы были еще ребенком,
и осталось с вами на всю жизнь?
Может и так...
Наконец настает время, когда вы уже больше не можете считать себя юнцом,
Больше ждать нельзя...
^;
!!IF:D99/z1/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99; Start up message

!!VRz1:S^ Вы отправляетесь на поиски счастья.
Ваша армия состояит только из одного человека, вашего друга Дэна.
Вместе вы захватите мир.^;
!!VRz3:S^%Z199\11_012.bmp^;
!!VRz4:S^ Это ваш друг Дэн^;
!!IF:D99/z1/z2/z2/z397/z3/z2/z2/z396/z4/z2/z2/z2/z2/z2/z2;
!!IF:E1/99; Start up message

!#OW:D6/254D1/254D0/254; Allow to live forever
!#OB26/27/0:S;

!#OB17/27/0:SHz402;
!#OB15/27/0:SHz403;
!#OB14/17/0:SHz404;
!#OB22/29/0:SHz406;
!#OB9/25/0:SHz407;
!#OB23/20/0:SHz409;
!#OB23/19/0:S;
!#OB22/19/0:S;
!#OB22/18/0:S;
!#OB18/23/0:Hz410;
!#HO2:SHz408;

Statue
!#TR18/23/0:P1E0; make an entrance
!#OB18/23/0:ST63U2; set a type (no matter)
!?OB18/23/0;
!!VRz3:S^%Z199\11_016.bmp^;
!!IF&v550<15:D99/z420/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF&v550>14/v550<30:D99/z437/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF&v550>29:D99/z438/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99;

Home
!?OB17/27/0;
!!VRz1:S^
Не так давно это был ваш дом.
Но это время прошло, и вы отправились на поиски счастья.
^;
!!VRz3:S^%Z199\11_001.bmp^;
!!IF:D99/z1/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99;

Dan's home
!?OB15/27/0;
!!VRz1:S^
Ваш друг Дэн решил сопровождать вас.
Ищите его в своей армии.
^;
!!VRz3:S^%Z199\11_002.bmp^;
!!IF:D99/z1/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99;

!?OB9/25/0;
!!VRz3:S^%Z199\11_017.bmp^;
!!IF:D99/z421/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99;
! !IF:M1/z421;

Exit sign
!#OB0/34/0:S;
!?OB0/34/0;
!!VRz1:S^
Поздравляем!
Вы нашли тайный проход к концу кампании.
^;
!!VRz3:S^%Z199\11_004.bmp^;
!!IF:D99/z1/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99;

Dead end sign
!#OB0/16/0:S;
!?OB0/16/0;
!!VRz1:S^
Dead End
^;
!!VRz3:S^%Z199\11_004.bmp^;
!!IF:D99/z1/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF:E1/99;

!?LE0/35/0; quick exit
!!HE-1:K;

Timer
!?TM1;
!!TM1&505:S1/1/100/0; disable timer
!!VRv500&v500=8:S9; second day
!!VRv500&v500=7:S8; next day
!!FU101:P0/20/26/0; if Hero at a spot
!!FU205&1:P; kill hero, he is at a spot
!!IF:V2/0;
!!FU102:P21/26/0/54/139; if peasant around place
!!FU102&-1:P19/26/0/54/139; if peasant around place
!!IF&1:V2/1;
!!TM2&2:S2/1/100/0; disable Dan's timer
!!VRv550&2:S35; disable Dan's message
!!FU101&2:P0/17/31/0; if he at place?
!!IF&1:V3/1;
!!VRv500&3/v500=6:S7; done
!!FU203&v500=7:P; all is ready - next state
!!FU204&2/v500<7:P; peasant is placed but hero did not hide
!!FU100&-2:P0/12/18/28/31/0; if he inside?
!!IF&-1/-2:Q1/21/7/1/z422;
!!OB26/27/0:R; enable a battle
!!FU201&v500=8:P; move Dan to field
!!FU202&v500=10:P; move Guards back
*** Timer subfunctions ***
*** kill hero, he is at a spot
!?FU205;
!!IF:Q1/21/7/1/z423;
!!HE0:T20/26/0/7/10;
*** all is ready - next state
!?FU203;
!!IF:Q1/21/139/1/z424;
!!IF:Q1/21/7/1/z425;
!!IF:Q1/21/139/1/z426;
!!UN:O26/27/0I22/26/0/54/7;
!!LE20/26/0:L1Mz427;
!!OB21/26/0:S;
!!OB19/26/0:S;
*** peasant is placed but hero did not hide
!?FU204;
!!IF:Q1/21/139/1/z428;
!!UN:O21/26/0O19/26/0I5/30/0/54/139;
!!MO5/30/0/1:G1O1U1R0;
!!UN:S5/30/0/6/2;
*** move Dan to the field
!?FU201;
!!UN:O21/26/0O19/26/0I5/30/0/54/139;
!!UN:O22/26/0I6/30/0/54/7;
!!UN:S6/30/0/6/2;
!!IF:Q1/21/139/1/z429;
*** move Guards back
!?FU202;
!!UN:O6/30/0I26/27/0/54/7;
!!MO26/27/0/1:G10O1U1R1;
**************************
Guards (0->1)
!?OB26/27/0;
!!IF&v500<3/-500:Q1/21/7/1/z430;
!!IF&v500<3/500:Q1/21/7/1/z431;
!!IF:V500/1;
!!IF&v500>1:Q1/21/7/1/z432;
!!HE-1&v500>1:T26/27/0/7/10;
!!TM1:Sc/999/1/64; set timer for pink
!!VRv500&v500<1:S1; guards visited
!!VRv550:S5; next Dan's state

Abandoned building (1->2)
!?OB14/17/0;
!!VRz3:S^%Z199\11_011.bmp^;
!!IF&-501:D99/z433/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF&-501:E1/99;
! !IF&-501:M1/z433;
!!VRz404&v500=1:Sz434;
!!VRv500&v500=1:S2; visited
!!IF&v500<>2/501:D99/z435/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF&v500<>2/501:E1/99;
! !IF&v500<>2/501:M1/z435;
!!IF&v500=2:D99/z436/z2/z2/z3/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2/z2;
!!IF&v500=2:E1/99;
! !IF&v500=2:M1/z436;
!!IF:V501/1;
!!VRv550&v500=2:S10; next Dan's state

Monk (2->3)
!?OB23/20/0;
!!IF&-502:Q1/21/8/1^ Благослови вас бог, %Z400.
В один прекрасный день я помогу вам.^;
!!VRz406&v500=2:S^ Водяная мельница Мельника^;
!!VRv500&v500=2:S3; Monk visited
!!IF&v500<>3/502:Q1/21/8/1^Да поможет тебе Бог, сын мой.^;
!!IF&v500=3:Q1/21/8/1^ Да, мы были знакомы. Богу известно, что он был праведным человеком.
Кирр покинул свой дом давным давно, и с тех пор его никто не видел.
Никто не знает, что с ним случилось...
Может быть, Мельник что-нибудь о нем слышал.^;
!!IF:V502/1;
!!VRv550&v500=3:S15; next Dan's state

Water Wheel (3->4)
!?OB22/29/0;
!!IF&-503:M^ Привет, %Z400, как поживаешь?^;
!!VRz408&v500=3:S^ Рыцарь Ратибор^; Change Name of Edrik
!!VRv500&v500=3:S4; Melnik visited
!!IF&v500<>4/503:M^ Это не подходящее место для того, чтобы рассиживаться.
Вряд ли стражники спят.
Вам лучше пойти
к тому полю пшеницы.^;
!!IF&v500=4:M^ Кирр... Точно не знаю...
Может, тому рыцарю известно больше.
Вы с ним говорили?
Его имя? Ну... Я могу ошибиться... Ратибор.^;
!!IF:V503/1;
!!VRv550&v500=4:S20; next Dan's state

Ratibor (4->5)
!?HE2;
!!IF&-504:M^ Убирайся, крестьянин!^;
!!VRv500&v500=4:S5; Melnik visited
!!IF&v500<>5/504:M^ Не испытывай мое терпение слишком долго!^;
!!IF&v500=5:M^ Откуда ты знаешь мое имя?
Ах, Мельник.
Этот мирный парень, слишком слаб, чтобы быть воином...^;
!!IF:D2/z210/0/z211/z212/z213/z214/z215/z216/z217/z218/z219/z220/z221/z222/z223;
!!IF:F2/z216/z217/z218/z219/0;
!!IF&v500=5:E1/2;
!!IF&v500=5/v1<>2:M^ Как ты смеешь...^;
!!HE-1&v500=5/v1<>2:K;
!!IF&v500=5/v1=2:M^ Отлично! Мне нужны свежие силы! Марш, марш!^;
!!HE2&v500=5/v1=2:P25/27/0;
!!UN&v500=5/v1=2:I1/16/0/54/0;
!!MO1/16/0/1&v500=5/v1=2:M-1;
!!MO1/16/0/1&v500=5/v1=2:G1O1U1R0;
!!IF:V504/1;
!!VRv550&v500=5/v1=2:S25; next Dan's state

Pikeman (5->6)
!?OB1/16/0;
!!IF&v500=5:Q1/21/7/1^ О, нет, Ратибор,
Мы не можем принять ваше предложение.
Нам приказано стоять здесь и смотреть,
чтобы эти глупые крестьяне работали как следует!
^Q1/21/0/1^ Мне уже слишком много лет, чтобы следовать за Ратибором.
С вами... что ж... только не очень быстро.^;
!!VRv500&v500=5:S6; Pikeman taken
!!HE2:P35/0/0;

!?LE20/26/0;
!!IF&v500>0:Q1/21/7/1^ Эй! Стойте, где стоите!
Мы идем!^;
!!IF&v550>=30/v550<35:Q1/21/139/1^ Ух!!!
Я дрожу от страха!
У меня ноги подкашиваются!
Я стою здесь!
Сделайте что-нибудь!!!^;
!!LE20/26/0&v500>0:M-1;

****** Dan's whine service *******
!#TM2:S1/999/1/64;
*** phase 30
!?TM2&v550>=30;
!!IF:Q1/21/139/1^ Я хочу домой,
пожааааалуйста!!!
Он смахивает слезу:
"Не помнишь, что говорил твой отец?"^;
*** phase 29
!?TM2&v550=29; !!VRv550:S30;
!!IF:Q1/21/139/1^ Я устал!
Я не могу больше...
Надо отдохнуть... Ох...^;
*** phase 28
!?TM2&v550=28; !!VRv550:S29;
!!IF:Q1/21/139/1^
Разве ты не видишь, что мы тут завязли!
Ты просто дурак!^;
*** phase 27
!?TM2&v550=27; !!VRv550:S28;
!!IF:Q1/21/139/1^ Я так устал.
Мы будем идти вечно!^;
*** phase 26
!?TM2&v550=26; !!VRv550:S27;
!!IF:Q1/21/139/1^ Ты не можешь идти помедленнее!^;
*** phase 25
!?TM2&v550=25; !!VRv550:S26;
!!IF:Q1/21/139/1^ Он разочаровал меня, этот Ратибор.
Нечего было на него рассчитывать...
Я весь в грязи.
Не трогай меня.
Больше я ничего не скажу!^;
*** phase 20
!?TM2&v550=20; !!VRv550:S21;
!!IF:Q1/21/139/1^ Ух ты, храбрый рыцарь!
Он наверняка поможет нам.
Как там его звали...
... ах, да, Ратибор!
Такое отличное имя...
Он хороший человек...
такой сильнный... такой вежливый...
^;
*** phase 15
!?TM2&v550=15; !!VRv550:S16;
!!IF:Q1/21/139/1^ Мельник... представления не имею.^;
*** phase 11
!?TM2&v550=11; !!VRv550:S12;
!!IF:Q1/21/139/1^ Ой, библия.
Тебе не кажется, что это грех, взять чью-то библию.
Что? Ах, за это...
Да, это хорошая идея.^;
*** phase 10
!?TM2&v550=10; !!VRv550:S11;
!!IF:Q1/21/139/1^ Я помню Кирра. Но...
не очень хорошо. Видите ли, мы тогда были так молоды.^;
*** phase 7
!?TM2&v550=7; !!VRv550:S8;
!!IF:Q1/21/139/1^ Нет, нет, мне не страшно,
но мне жутко.^;
*** phase 6
!?TM2&v550=6; !!VRv550:S7;
!!IF:Q1/21/139/1^ Я думаю об этих стражниках.
Может быть есть другой, простой выход.^;
*** phase 5
!?TM2&v550=5; !!VRv550:S6;
!!IF:Q1/21/139/1^ Эти стражники на вид ужасные.
Но мы не боимся...
Правда?^;
*** phase 2
!?TM2&v550=2; !!VRv550:S3;
!!IF:Q1/21/139/1^ Что мы ищем?^;
*** phase 1
!?TM2&v550=1; !!VRv550:S2;
!!IF:Q1/21/139/1^ Вместе мы сможем победить наших врагов!^;
*** phase 0
!?TM2&v550=0; !!VRv550:S1;
!!IF:Q1/21/139/1^ Мы настоящие друзья, %Z400,
и мы останемся вместе
до самого конца...
Верно?^;
***********************************

Out
!?LE27/27/0;
!!IF:V505/1;
!!VRv500&v500>7/v500<10:S10; move the guard back

****** Functions ******
Check if hero x1 inside square x2...x3*x4...x5 level x6 and set Flag1 if yes
!?FU100;
!!HEx1:P?y1/?y2/?y3; store hero's position
!!IF:V1/0; clear flag
!!IF&y1>=x2/y1<=x3/y2>=x4/y2<=x5/y3=x6:V1/1; set flag if Ok

Check if hero x1 at position x2*x3 level x4 and set Flag1 if yes
!?FU101;
!!HEx1:P?y1/?y2/?y3; store hero's position
!!IF:V1/0; clear flag
!!IF&y1=x2/y2=x3/y3=x4:V1/1; set flag if Ok

Check for object type x4 and subtype x5 at position x1/x2/x3
!?FU102;
!!IF:V1/0; clear flag
!!VRv2:Sx1; !!VRv3:Sx2; !!VRv4:Sx3;
!!OB2:T?y1U?y2; use v2/v3/v4 to point
!!IF&y1=x4/y2=x5:V1/1; set flag if Ok

**********************
****** Part II *******
!#VRz320:S^Вершины - близнецы (левая часть)^;
!#VRz321:S^Вершины - близнецы (правая часть)^;

!#OB29/26/0:SHz320;
!#OB30/27/0:SHz321;
!#OB32/30/0:S;
!#OB33/31/0:S;
!#OB34/30/0:S;
!#OB35/29/0:S;
!#OB33/28/0:S;
!#OB34/28/0:S;
!#OB34/23/0:S;
!#OB24/2/0:S;
!#OB33/19/0:S;
!#OB19/4/0:S;
!#OB34/10/0:S;
!#OB34/16/0:S;
!#OB24/12/0:S;
!#OB31/25/0:S;
!#OB25/14/0:S;
!#OB3/2/0:S;
!#OB21/6/0:S;
!#OB27/17/0:S;
!#OB35/23/0:S;
!#OB30/11/0:S;
!#OB29/12/0:S;
!#OB29/14/0:S;
!#OB30/12/0:S;
!#OB35/22/0:S;

!#VRv350:S0; global state
!#VRv360:S0; sequence state
!#IF:V360/0; sequence flag
!#VRz360:S^^;   sequence name
!#VRv361:S0; castle visit state
!#VRv362:S0; rogue visit state
!#VRv370:S0; completed tasks

Clear all flags and vars
!#IF:V300/0V301/0V302/0; rally flag

Out (continue)
!?LE27/27/0;
!!TR30/25/0:P1;
!!TR30/21/0:P1;
!!TR31/22/0:P1E1;
**************************
#1
!?OB32/30/0; !!VRv360:S1; !!IF:V360/1; !!VRz360:S^ Звездочет^;
!!IF:Q1/21/138/1^ Привет, франт. Что?... Ладно, %Z400.
Знаешь, %Z400, жизнь коротка, и мы должны испробовать все, что она предлагает...
Эй, франт... э... %Z400, ты из... что? Какое смешное имя, ха!
Ладно, ладно. О чем, бишь я? ...
О, да! Жизнь коротка, и мы должны испробовать все, что она предлагает...
... ты знаешь? ... уже? ... ладно. Не сердись! Расслабься.
Мой брат... Ты моего брата знаешь? Нет? Он живет к северу отсюда...
Вот... О чем это я?
Ах, да, мой брат. Он обычно говорил мне, что жизнь коротка
и мы должны испробовать все, что она предлагает... Ой! Ты совсем как мой брат!
Эй, франт... ладно, %Z400. Итак, тебе, %Z400, обязательно надо навестить моего брата.
Вы найдете, о чем поговорить. Особенно насчет того, что жизнь коротка и...
Ладно, ладно, ступай. Передай ему от меня привет... а зовут его Звездочет.^;
!!UN:O32/30/0;

#2
!?OB33/31/0; !!VRv360:S2; !!IF:V360/1; !!VRz360:S^ Влад^;
!!IF:Q1/21/139/1^ Не слушай их. Они ничего не знают о жизни.
А вот мой брат знает!
Это было давно. Мы были юными и амбициозными...
Рыцарь проезжал через нашу деревню. Такой странный рыцарь...
Вообрази, когда мой брат предложил ему свои услуги,
он на него даже и не взглянул!
Странный рыцарь...
Кто тогда мог знать ...
Они не ушли далеко... их побили.
Может, мой брат поможет вам избежать их участи.
Найдите его. Его зовут Влад. Удачи!
^;
!!UN:O33/31/0;

#3
!?OB34/30/0; !!VRv360:S3; !!IF:V360/1; !!VRz360:S^ Гавриил^;
!!IF:Q1/21/9/1^ Сын мой, будь терпеливым.
Не позволяй гневу завладеть твоим сердцем.
Бог поможет тебе. Так... так...
Не хочешь тост?
У меня есть отличное красное вино.
А! То есть было...
Тебе надо зайти к моему двоюродному брату, Гавриилу.
У него вино еще лучше, поверь мне.^;
!!UN:O34/30/0;

#4
!?OB35/29/0; !!VRv360:S4; !!IF:V360/1; !!VRz360:S^Игорь^;
!!IF:Q1/21/1/1^ Не слушай ты этих гражданских.
Только настоящий воин может быть объективным...
особенно если он пьян, ха!
Да, я воин, Игорь. Как тебя зовут, Герой!
Я смотрю, ты совсем как настоящий воин, %Z400.
Ты мой друг, а для друга я что угодно сделаю!
Слушай. Есть место, недалеко отсюда ... да.
И там ты найдешь одну очень полезную вещь
для настоящего воина, вроде тебя!
Увидимся позже!^;
!!UN:O35/29/0;

#5
!?OB33/28/0; !!VRv360:S5; !!IF:V360/1; !!VRz360:S^ Торговец^;
!!IF:Q1/21/28/1^ Привет, купец!
Ты знаешь, какова сила денег?
Ты мне не веришь? Правда?
Ты такой дурак!
Тогда иди на рынок и сошлись на меня, Торговца.
И ты увидишь!^;
!!UN:O33/28/0;

#6
!?OB34/28/0;
!!IF:V1/0; !!VRv1:Sv361&16; !!IF&v1<>0:V1/1; test for bit 4
!!IF&-1:M^ Здравствуй, славный рыцарь.
Бери стул и выпей с нами.
Если ищешь способ начать карьеру,
лучше всего отправиться в замок
и попросить работу там.
Ближайший замок к северу отсюда. Попробуй.^;
!!VRv361&-1:|1; set bit 1
!!IF&1:M^ Привет,%Z400.
Ну как, нашли работу?
Нет? Так присоединяйся к нам!
Быть беглым рыцарем не так уж плохо.
Мы все ищем славы!^;

#7
!?OB34/23/0;
!!IF:V1/0; !!VRv1:Sv362&2; !!IF&v1<>0:V1/1; test state
!!IF&1:Q3/21/143/1^Золото?
Без стражи? Невероятно!
Вы не могли бы побыть здесь, присмотреть за молим лагерем и за костром...
Я скоро вернусь! Наверное...^;
!!VRv362&1:|4;
!!UN&1:O34/23/0O30/12/0;
!!IF&-1:Q3/21/143/1^ Вы кто?
Рыцарь? А с виду больше похожи на крестьянина.
Откуда я знаю??
Что ж, не так давно мне пришлось повоевать в армии у рыцаря вроде вас,
но нас скоро разбили.
Не так далеко отсюда...
И теперь я понимаю, что вы ничего не можете сделать
без хитрости.^;
!!VRv362&-1:|1;

#8
!?OB24/2/0;
!!VRz3:S^%Z199\11_sun.avi^;
!!IF:B100/z3/1;
!!IF:P100;
!!IF:Q1/21/93/1^ Вы никогда не видели говорящей птицы?
Верно, птицы не могут говорить. Но я не птица,
я Жезл, %Z400.
Я все знаю о тебе. Обо всех твоих заслугах и неудачах,
о твоих мыслях и намерениях, твоих долгах и обещаниях.
Все про тебя.
И помяни мое слово.
Корень зла, терзающего мир Героев
в недостатке веры в справедливость, в истину,
в Бога!^;
!!VRv350:S1;

#9
!?OB33/19/0;
!!IF:V1/0; !!VRv1:Sv361&2; !!IF&v1<>0:V1/1; test for bit 2
!!IF&1:Q2/21/139/1^ Вы не можете добраться до замка?
Мой сын служит там в дворцовой страже.
Передайте привет моему сыну,
когда в следующий раз попытаетесь добраться туда.^;
!!VRv361&1:|4; set bit 3
!!IF&-1:Q2/21/139/1^ Оставьте меня в покое.
Вы что, не видите, что я работаю!
Копать норы не так легко.^;

#9a
!?OB21/6/0;
!!IF&1:Q2/21/0/1^ Стойте, где стоите!^;
!!VRv361:|2; set bit 2
!!IF:V1/0; !!VRv1:Sv361&4; !!IF&v1<>0:V1/1; test for bit 3
!!TR20/5/0&1:P1;
!!TR21/6/0&1:P1E1;
!!IF&1:Q2/21/0/1^ Да, мой отец. Мне очень его не хватает...
Вы можете пройти.^;
!!VRv361&1:&253; reset bit 2
!!IF&-1:Q2/21/0/1^ Кто вы? Я вас не знаю.
Все потеряно!^;

#10
!?OB19/4/0;
!!IF:Q2/21/6/1^ Именем короля. Стой!^;
!!IF:V1/0; !!VRv1:Sv361&16; !!IF&v1<>0:V1/1; test for bit 4
!!IF&1:Q2/21/6/8/4/1^ Эй, гляньте-ка!
Он украл нашу баллисту!
Что за черт...
Именем Короля: Взять его!!!^;
!!HE-1&1:T19/4/0/6/10;
!!IF:V1/0; !!VRv1:Sv361&1; !!IF&v1<>0:V1/1; test for bit 1
!!IF&1:Q2/21/6/1^ Xочешь получить работу?
Ладно, крестьянин..  Пойди и отыщи наш потерянный транспорт,
и привези его нам.
Где?
А вы уверены, что вы рыцарь?^;
!!VRv361&1:|8; set bit 4
!!IF&-1:Q2/21/6/1^ Убирайся отсюда, грязный оборванец!^;

$1
!?OB34/10/0&v360=1/-360; Already
!!IF:Q1/21/34/1^ Я ничем не могу вам помочь.
Уходите. Я наблюдаю за звездами.^;
!?OB34/10/0&v360=1/360; Ладно
!!IF:Q1/21/34/2^ Да, я Звездочет.
Мой брат, этот горький пьяница???
Он обещал мою помощь??
Я не знаю... ну...
Если вы дадите мне слово, что поможете,
когда я буду в беде, я....
{Вы согласны?}^;
!!IF&1:D8/z200/0/z201/z202/z203/z204/z205/z206/z207/z208/z209/z206/z207/z208/z209;
!!IF&1:F8/z206/z207/z208/z209/0;
!!IF&1:E1/8;
!!IF&1:V2/0;
!!VRv2&1/v1=1:S1; !!VRv2&1/v1=2:S2; !!VRv2&1/v1=3:S3; !!VRv2&1/v1=4:S4;
!!IF&v2=0:V1/0; escape
!!VRv370&1:+1; task completed
!!VRv701&1:S1;
!!HE-1&1/v2=1:S14/1M31/1M62/1M21/1;
!!IF&1/v2=1:Q3/20/45/1^ Вы берете книгу и кладете ее в карман.^;
!!IF&1/v2=1:Q3/9/31/9/62/9/21/1^ И вы обнаружили в книге следующие заклинания:^;
!!HE-1&1/v2=2:S17/1M46/1M33/1M27/1;
!!IF&1/v2=2:Q3/20/54/1^ Вы берете книгу и кладете ее в карман.^;
!!IF&1/v2=2:Q3/9/46/9/33/9/27/1^ И вы обнаружили в книге следующие заклинания:^;
!!HE-1&1/v2=3:S15/1M30/1M47/1M28/1;
!!IF&1/v2=3:Q3/20/48/1^ Вы берете книгу и кладете ее в карман.^;
!!IF&1/v2=3:Q3/9/30/9/53/9/28/1^ И вы обнаружили в книге следующие заклинания:^;
!!HE-1&1/v2=4:S16/1M32/1M45/1M20/1;
!!IF&1/v2=4:Q3/20/51/1^ Вы берете книгу и кладете ее в карман.^;
!!IF&1/v2=4:Q3/9/32/9/45/9/20/1^ И вы обнаружили в книге следующие заклинания:^;
!!IF&1:V360/0;
!!IF&-1:Q1/21/34/1^ Ладно. Это ваш выбор, %Z400.^;
!?OB34/10/0&v360<>1;
!!VRz1&360:S^ Привет, незнакомец.
Я не знаю... Кто это %Z360^;
!!VRz1&-360:S^ Привет, %Z400.
Звезды всегда говорят. Надо только прислушаться к ним.^;
!!IF:Q1/21/34/1/z1;

$2
!?OB34/16/0&v360=2/-360; Already
!!IF:Q1/21/139/1^ Не мешайте мне.
Не видите, что я работаю?^;
!?OB34/16/0&v360=2/360; Ok
!!IF:Q1/21/139/2^ Мой брат... да, я Влад.
Были дни...
Вы напоминаете мне того рыцаря. Как его звали...
Да-а, Кирр. Вы его знаете?
Да, у меня есть для вас нечто особенное.
Но... только если вы поможете мне, когда я буду в этом нуждаться.

{Ладно?}^;
!!VRz3&100/1:S^%Z199\11_sword.avi^;
!!IF&100/1:B100/z3/1;
!!IF&100/1:P100;
!!IF&1:Q2/8/8/21/139/1^ Это меч Кирра. Возьмите его.^;
!!HE-1&1:A1/8/3;
!!VRv370&1:+1; task completed
!!VRv702&1:S1;
!!IF&1:V360/0;
!!IF&-1:Q1/21/139/1^ Делайте, что хотите, %Z400.^;
!?OB34/16/0&v360<>2;
!!VRz1&360:S^ Привет, незнакомец.
А откуда мне знать, что это %Z360^;
!!VRz1&-360:S^ Привет, дружище.
Взгляните на мои цветы. Чудесные, правда?^;
!!IF:Q1/21/139/1/z1;

$3
!?OB24/12/0&v360=3/-360; Already
!!IF:Q1/21/8/1^ Бог знает!^;
!?OB24/12/0&v360=3/360; Ok
!!IF:Q1/21/8/2^ Кто? Я его не знаю.
Xотя все мы дети Божьи...
Но некоторые больше, чем другие!
А вам, мирянин, следовало бы продемонстрировать свою покорность.
Если вы обещаете подчиняться законам Божьим,
я подарю вам подарок.

{Вы с Богом?}^;
!!IF&1:Q2/8/58/21/8/1^ Этот плащ -- дар Божий.^;
!!HE-1&1:A1/58/1;
!!VRv370&1:+1; task completed
!!VRv703&1:S1;
!!IF&1:V360/0;
!!IF&-1:Q1/21/8/1^Бог благословил вас!^;
!?OB24/12/0&v360<>3;
!!VRz1&360:S^ Бог тебе в помощь, сынок.
%Z360 неизвестен ни мне, ни церкви.^;
!!VRz1&-360:S^ Господь благословит вас!^;
!!IF:Q1/21/8/1/z1;

$4
Rally flag
!?OB31/25/0&v360=4/360; Ok
!!IF:M^ Памятуя слова Игоря, вы решате
потратить время на осмотр окрестностей.^;
!!VRz3&100:S^%Z199\11_shield.avi^;
!!IF&100:B100/z3/1;
!!IF&100:P100;
!!IF:Q1/8/15/1^ Может быть это щит Кирра^;
!!HE-1:A1/15/4;
!!VRv370&1:+1; task completed
!!VRv704&1:S1;
!!IF&1:V360/0;
!?OB31/25/0&v360<>4; first visit
!!IF:M^ Да, это флаг Сбора...
был когда-то... А теперь это жалкое зрелище.
Повсюду черепа и кости...
Что-то тут знакомое.^;

$5
!?OB25/14/0;
!?OB25/14/0&v360=5/360; Ok
!!IF:M^ Имя Торговца имеет на рынке особую ценность.
Xотя вы никогда не обладали торговыми навыками,
вы не можете устоять перед этим предложением.^;
!!IF:Q1/8/3/1^ Это катапульта!^;
!!HE-1:A1/3/16;
!!VRv370&1:+1; task completed
!!VRv705&1:S1;
!!IF&1:V360/0;
!?OB25/14/0&v360<>5; Not
!!IF:M^ Вам что-нибудь нужно?^;

$8
!?OB3/2/0&v350=0;
!!IF:Q1/21/168/1^ Привет, смертный!
Пока, смертный!!!^;
!!HE-1:T3/2/0/168/1;
!?OB3/2/0&v350=1;
!!IF:Q1/21/168/2^ Привет, смертный!
Что? Бог?
Да, я Бог...
то есть...
Ну, собственно я не Бог,
а преедставитель Бога на Земле.

{Вы мне верите?}^;
!?OB3/2/0&v350=1/1;
!!IF:Q3/21/168/1^ Это правильно.
Боги велели мне идти с тобой и помочь
тебе в твоем квесте.
Так что, веди!^;
!!MO3/2/0:R0;
!!OB3/2/0:R;
!!IF:M^ Поздравляем!
Вы выполнили главную миссию
и %V370 из 7 подмиссий.^;
!!VRv700:Sv370;
!!VRz3:S^%Z199\11_out.avi^;
!!IF:B100/z3/1;
!!IF:P100;
!?OB3/2/0&v350=1/-1;
!!IF:Q3/21/168/1^Вы самонадеяный глупец!^;
!!HE-1:T3/2/0/168/1;

$10
!?OB27/17/0;
!!IF:V1/0; !!VRv1:Sv361&8; !!IF&v1<>0:V1/1; test for bit 4
!!IF&1:M^ Вспоминая слова стража у дворцовых ворот,
вы замечаете, что повозка похожа на транспорт,
о котором он говорил.
А он сказал, там может быть что-нибудь полезное.^;
!!IF&1:Q2/8/4/1^ Здорово! Вы нашли Баллисту!
Тот страж говорил, что вы должны вернуть ее ему.
Никогда! Он был слишком грубый!
Верно, оставьте ее себе.^;
!!HE-1&1:A1/4/13;
!!VRv370&1:+1; task completed
!!VRv706&1:S1;
!!VRv361&1:S1+16; reset all
!!IF&-1:M^ Старая повозка.
Вы решаете не тратить время на копание
в мусоре.^;

!?OB35/23/0;
!!IF:V1/0; !!VRv1:Sv362&4; !!IF&v1<>0:V1/1; test state
!!OB35/23/0&1:R;
!!IF&-1:Q3/21/143/1^ Эй!
Не гаси мой костер!
Ты же не хочешь, чтобы я замерз?^;

!?OB35/22/0;
!!IF:V1/1; !!VRv1:Sv362&8; !!IF&v1<>0:V1/0; test state
!!IF&-1:M^ Ничего больше...^;
!!IF&1:Q1^ Пытаешься украсть у вора?

{Ты что, тоже вор?}^;
!!IF&1:Q1/8/0/1^ Ладно... ты нашел книгу.
Магическую книгу!^;
!!HE-1&1:A1/0/17;
!!VRv370&1:+1; task completed
!!VRv707&1:S1;
!!VRv362&1:|8;

!?OB30/11/0; !!FU300:P;
!?OB29/12/0; !!FU300:P;
!?OB29/14/0; !!FU300:P;
!?FU300;
!!IF:M^ Тишина...
Ты подходишь ближе и поднимаешь руку,
чтобы постучать в дверь...
Но нет... ты слышишь звук...
Точно. Это храп!
Великолепно. Ну и рабочие! Спят весь день.
Будет ли мудро их будить?
^;

!?OB30/12/0;
!!IF:V1/0; !!VRv1:Sv362&1; !!IF&v1<>0:V1/1; test state
!!IF&1:M^ Ситуация выглядит просто как мечта вора.^;
!!VRv362&1:|2;
!!IF&-1:M^ Это твое золото, %Z400?^;
