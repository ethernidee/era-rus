ZVSE

* String
!#VRz127:S^%Z199\21_023.BMP^; Cordelia a faerie
!#VRz128:S^%Z199\21_024.BMP^; Mathias the architect
!#VRz129:S^%Z199\21_025.BMP^; Jack the watherwheel

!#VRz132:S^%Z199\21_030.BMP^; Cain the miner
!#VRz133:S^%Z199\21_028.BMP^; Alexey the player
!#VRz134:S^%Z199\21_029.BMP^; the miller

!#VRz424:S^Корделия, фея^;
!#OB32/26/0:SHz424;

!#VRz425:S^Дом Матиаса, архитектора^;
!#OB27/21/0:SHz425;

!#VRz427:S^Водопад
Долм Корделии^;
!#OB35/23/0:SHz427;

!#VRz428:S^Джек^;
!#OB26/23/0:SHz428;

!#VRz429:S^Каин, шахтер^;
!#OB27/26/0:SHz429;

!#VRz430:S^Дима, мельник^;
!#OB29/30/0:SHz430;

!#VRz431:S^Алексей, игрок^;
!#OB34/30/0:SHz431;

!#VRz432:S^Таверна^;
!#OB34/29/0:SHz432;

!#VRz433:S^Архитектор Матиас^;

!#VRv528:S0; state of architect

* Variable
!#VRv529:S0; Cordelia and source
!#VRv530:S0; Jack and Waterwheel
!#VRv531:S0; mine
!#VRv532:S0; miller
!#VRv533:S0; State of Gaпa
!#VRv539:S0; State of Gaпa 1
!#VRv535:S0; State of Gaпa 2
!#VRv536:S0; State of Gaпa 3
!#VRv537:S0; State of Gaпa 4


*** Mathias the architect ***
!?OB27/21/0; house of the architect...
!!VRz212:S^Меня зовут Матиас.

Я был архитектором.

Пожалуйста, спасите мой город...
И уничтожьте ужасных монстров.^;
!!VRz212&v528=1:S^
Пожалуйста, спасите мой город...

Гайа с вами.^;
!!VRz212&v528=2:S^Спасибо, что спасли мой город.^;
!!VRv528&v528=0:S1;
!!FU16:Pz212/z128/z433; mathias dialogue

*** Town as a trigger troydaпo ***
!?OB31/23/0; Town troydaпo...
!!VRv539:S1; State of Gaпa 1
!!VRv528&v528<2:S2;

*** Cordelia the faerie ***
!?OB32/26/0;
!!VRv529&v529=0:S1;
!!VRz200:S^Здравствуйте
И снова вы нужны нам.
Какие-то призраки отравили наш источник.
Пожалуйста, согласитесь убить их.^;
!!FU16:Pz200/z127/z424; Cordelia dialogue
!!VRz207:S^I'll do it...^;
!!FU16:Pz207/z125/z400; heroes dialogue

*** Wraith ***
!?OB34/24/0;
!!IF:Q1/21/61/1^Никогда!
Никогда больше источник не будет принадлежать лесному народу.
Мы убьем вас всех...^;
!!UN:O32/26/0/1;

*** The Source ***
!?OB35/23/0;
!!VRv529&v529=0:S1;
!!VRz200:S^Спасибо^;
!!FU16:Pz200/z127/z424; Cordelia dialogue
!!FU33&v529=1:P;

*** FUNCTION 33 : Dialogue
!?FU33;
!!VRv529:S2;
!!VRv535:S1; State of Gaпa 2
!!VRz200:S^Пожалуйста, призовите Гайю, чтобы снова очистить нашу землю от призраков...^;
!!FU16:Pz200/z127/z424; Cordelia dialogue
!!VRz200:S^Дорогой ученик... Мы снова нужны Эрафии...
Закрой глаза... Вперед...^;
!!FU16:Pz200/z110/z408; Gaпa dialogue
!!FU13:P25/21/35/30/0/2/50; big square
!!FU13:P28/31/35/32/0/2/60; line
transform grass the better I can
!!TR24/25/0:T2/24/d/d/d/d/d/d;
!!TR28/20/0:T2/29/d/d/d/d/d/d;
!!TR24/26/0:T2/24/d/d/d/d/2/d;
!!TR24/27/0:T2/25/d/d/d/d/2/d;
!!TR24/28/0:T2/26/d/d/d/d/2/d;
!!TR24/29/0:T2/27/d/d/d/d/2/d;
!!TR26/31/0:T2/35/d/d/d/d/1/d;
!!TR32/32/0:T2/28/d/d/d/d/3/d;
!!TR33/32/0:T2/29/d/d/d/d/3/d;
!!TR34/32/0:T2/30/d/d/d/d/3/d;
!!TR35/32/0:T2/31/d/d/d/d/3/d;
!!TR28/33/0:T2/36/d/d/d/d/d/d;
!!TR29/33/0:T2/30/d/d/d/d/3/d;
!!TR30/33/0:T2/37/d/d/d/d/d/d;
!!TR28/32/0:T2/26/d/d/d/d/d/d;
!!TR27/31/0:T2/28/d/d/d/d/d/d;
!!TR30/32/0:T2/39/d/d/d/d/0/d;
!!TR31/32/0:T2/28/d/d/d/d/3/d;
!!TR28/31/0:T2/34/d/d/d/d/d/d;
!!TR25/30/0:T2/34/d/d/d/d/1/d;
!!TR23/23/0:T2/39/d/d/d/d/3/d;
!!TR23/24/0:T2/36/d/d/d/d/2/d;
!!TR24/23/0:T2/51/d/d/d/d/d/d;
!!TR24/22/0:T2/39/d/d/d/d/3/d;
!!TR27/20/0:T2/37/d/d/d/d/0/d;
!!TR29/20/0:T2/37/d/d/d/d/2/d;
!!TR25/21/0:T2/39/d/d/d/d/3/d;

*** the waterwheel
!?OB26/23/0; jack
!!VRi:S0;
!!OW&v530=0:R0/5/?i;
!!VRv530&i>=3:S1;
!!VRz213:S^Меня зовут Джек...

Если бы только у меня было три драгоценных камня, я бы смог починить свое водяное колесо.^;
!!VRz213&v530=1:S^Меня зовут Джек, я работаю на водяной мельнице.

Вы согласны дать мне три драгоценных камня?^;
!!FU16:Pz213/z129/z428; Jack
!!FU34&v530=1:P;

*** Function 34 : Place the waterwheel
!?FU34;
!!UN:O26/23/0; Delete Jack
!!VRv536:S1; State of Gaпa 3
!!UN:I25/23/0/109/0/109/0/2; Place the Waterwheel
!!OW:R0/5/d-3;

*** Alexey
!?OB34/30/0; alexey
!!VRz213:S^Меня зовут Алексей...
Я игрок в кости.
Пожалуйста, зайдите в трактир и сыграйте со мной.^;
!!FU16:Pz213/z133/z431; Alexey
!!UN:O34/30/0; delete Alexey
!!IF:V1/1;

*** The tavern ***
!#VRv303:S0;
!?OB34/29/0; Tavern
!!VRz213&1:S^Я Алексей, игрок в кости.

Пожалуйста, сыграйте со мной...

Ставка 1000 золотых, и вы сможете выиграть немного драгоценных камней.^;
!!FU16&1:Pz213/z133/z431; Alexey
!!IF:Q2^Xотите сыграть со знаменитым игроком в кости?^;
!!IF:V1/0; condition to false
!!VRv300:S0;
!!VRv304:S0;
!!OW:R0/6/?v300; check gold
!!IF&v300>=1000:V1/1;
!!IF&-1:M^К несчастью, у вас недостаточно денег.^;
!!HE-1:W?v304;
!!IF&v304=0:M^У вас нет времени играть. Может быть, завтра.^;
!!IF&v304=0:V1/0;
!!IF&-2:V1/0;
!!FU35&1:P;

*** Function 35 : Dice Playing
!?FU35;
!!VRv301:S2R10; dice player
!!VRv302:S2R10; dice computer
!!VRv303:S0; state of game
!!VRv303&v301=v302:S0; deuce
!!VRv303&v301>v302:S1; player win
!!VRv303&v301<v302:S2; player lose
!!VRz213&v303=0:S^
Кости

Очки Алексея : %V302                         Ваши очки : %V301
^;
!!VRz213&v303=1:S^
ВЫ ПОБЕДИЛИ...

Вот ваш драгоценный камень...

Очки Алексея : %V302                         Ваши очки : %V301
^;
!!VRz213&v303=2:S^
ВЫ ПРОИГРАЛИ...

Дайте мне 1000 золотых. Спасибо

Очки Алексея : %V302                         Ваши очки : %V301
^;
!!VRz2:S^^;
!!IF:D99/z213/z2/z2/z133/z125/z2/z2/z431/z400/z2/z2/z2/z2/z2/z2;
!!IF:E1/99;
!!OW&v303=1:R0/5/d+1;
!!OW&v303=2:R0/6/d-1000;
!!HE-1:Wd-100; remove 8 movement
!!IF:V1/0; condition to false

*** Cain and the mine ***
!?OB27/26/0;
!!VRz214:S^Привет, меня зовут Каин...

Я шахтер...
Мою шахту захватили троглодиты.
Пожалуйста, убейте их
и я поделюсь с вами кристаллами.^;
!!FU16:Pz214/z132/z429; Cain Dialogue
!!UN:O27/26/0; delete Cain

!?OB26/25/0;
!!VRv531:S1; mine
!!VRz214:S^
Спасибо за вашу помощь.^;
!!FU16:Pz214/z132/z429; Cain Dialogue

!?OB29/30/0;
!!VRi:S0;
!!OW&v532=0:R0/4/?i;
!!VRv532&i>=3:S1;
!!VRz215:S^Привет, меня зовут Дима...

Я мельник...
Друзья, вы нужны мне
Пожалуйста, поделитесь тремя кристаллами...
Может, Каин вам поможет.^;
!!VRz215&v532=1:S^Привет, меня зовут Дима...

Я мельник... Мне нужны ваши три кристалла.
Пожалуйста, дайте их мне.^;
!!VRz215&v532=2:S^ Спасибо... Гайа с вами.^;
!!FU16:Pz215/z134/z430; Dima
!!FU37&v532=1:P;

*** Function 37 : Place the windmill
!?FU37;
!!UN:O26/29/0; Delete the windmill
!!VRv537:S1; State of Gaпa 4
!!UN:I28/30/0/63/8; Place the windmill
!!OW:R0/4/d-3; remove three
!!VRv532:S2;

*** Temple of Gaпa
!#VRz50:S^Xрам Гайи^;!#OB1/7/0:SHz403; Troll.
!#OB29/32/0:SHz50; Disable object's normal function as a crypt.

!?OB29/32/0;
!!FU38:P;

*** Function 38 : Determine if Samaritan is ready
!?FU38;
!!VRv533:S0;
!!VRv533&v539=1:+1;
!!VRv533&v535=1:+1;
!!VRv533&v536=1+1;
!!VRv533&v537=1:+1;
!!VRz200:S^Этой стране нужна твоя помощь...

Возвращайся, когда поймешь, что
долг выполнен, и все дела сделаны...
Для последнего испытания тебе не нужна будет армия...^;
!!VRz200&v533=4:S^Мой сын,

Чтобы стать моим учеником, тебе надо очистить свою душу...
Есть только один способ :

Вы готовы для последнего испытания?^;
!!FU16:Pz200/z110/z408; Gaпa dialogue
!!FU39&v533=4:P;

*** Function 39 : The final battle
* Place flowers
!?FU39;
!!UN:I4/14/0/120/0; add flower
!!UN:I4/15/0/120/0; add flower
!!UN:I10/1/0/26/0; place event
!!HE-1:P9/1/0;
* Settings for nemesis
!!HE16:R0/3 R1/3; luck and morale
!!HE21:R0/3 R1/3; luck and morale
!!VRv15:S0;
!!VRv16:S0;
!!VRv17:S0;
!!VRv18:S0;
!!HE21:F?v15/?v16/?v17/?v18;
!!HE16:Fv15/v16/v17/v18; Copy primary skill
!!VRv18:*10;
!!HE16:Iv18;
!!HE21:Iv18;
!!VRv19:S0;
!!VRv20:S0;
!!VRv21:S0;
!!VRv22:S0;
!!VRv23:S0;
!!VRv24:S0;
!!VRv25:S0;
!!VRv26:S0;
!!VRv27:S0;
!!VRv28:S0;
!!VRv29:S0;
* check Secondary skill
!!HE21:S14/?v19; Check if heroes have fire magic
!!HE21:S15/?v20; air
!!HE21:S16/?v21; water
!!HE21:S17/?v22; earth
!!HE21:S1/?v23; archery
!!HE21:S23/?v24; armorer
!!HE21:S22/?v25; offense
!!HE21:S26/?v26; resistance
!!HE21:S25/?v27; sorcery
!!HE21:S19/?v28; tactic

!!HE16:S14/v19; Give nemesis if heores have : fire magic
!!HE16:S15/v20; air
!!HE16:S16/v21; water
!!HE16:S17/v22; earth
!!HE16:S1/?v23; archery
!!HE16:S23/?v24; armorer
!!HE16:S22/?v25; offense
!!HE16:S26/?v26; resistance
!!HE16:S25/?v27; sorcery
!!HE16:S19/?v28; tactic

!!HE16:C0/0/151/2;  Give Diamond Dragon
!!HE16:C0/1/132/3;  Give Azure Dragon
!!HE16:C0/2/134/2;  Give Faerie Dragon
!!HE16:C0/3/168/3; Give Gorynch  Dragon
!!HE16:C0/4/133/3;  Give Crystal Dragon
!!HE16:C0/5/135/2;  Give Rust Dragon
!!HE16:C0/6/155/2; Give Darkness Dragon
!!HE16:P7/11/0;  move heroe to final destination

!?LE10/1/0; give monster for the final battle
!!LE10/1/0:L1; Cancel event after first visit
!!VRz200:S^Мой ученик,

Мы должны победить все зло внутри себя.
Готовься сразиться с собственной судьбой.

Если ты победишь, то останешься моим учеником
м поможешь мне спасти Эрафию от уничтожения...

Если нет... Армагеддон станет реальностью!^;
!!FU16:Pz200/z110/z408; Gaпa dialogue
!!LE10/1/0:C0/151/2;  Give Diamond Dragon
!!LE10/1/0:C1/132/3;  Give Azure Dragon
!!LE10/1/0:C2/134/2;  Give Faerie Dragon
!!LE10/1/0:C3/168/3; Give Gorynch  Dragon
!!LE10/1/0:C4/133/3;  Give Crystal Dragon
!!LE10/1/0:C5/135/2;  Give Rust Dragon
!!LE10/1/0:C6/155/2; Give Darkness Dragon
!!HE-1:W0; Change movement to 0