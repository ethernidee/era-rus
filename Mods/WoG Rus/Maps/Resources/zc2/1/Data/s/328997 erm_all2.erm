ZVSE
!#VRz110:S^%Z199\21_006.BMP^; Gaпa
!#VRz130:S^%Z199\21_026.BMP^; Ma the troll

!#UN:P5/0;
!#UN:J3/^:clear:^;
!#VRz408:S^Гайа, Повелитель Деревьев^;
!#OB2/15/0:SHz408; the tree of knowledge

!#VRz403:S^Психомартышка, Тролль Екатерины.^;
!#OB1/7/0:SHz403; Troll.

* variable
!#VRj:S8; Goblins number
!#VRv507:S0; Troll
!#VRv508:S0; F R E E
!#VRv509:S0; State of Timer
!#VRv510:S0; Random teleporter
!#VRv511:S0; goblins 1 alive
!#VRv512:S0; goblins 2 alive
!#VRv513:S0; goblins 3 alive
!#VRv514:S0; goblins 4 alive
!#VRv515:S0; goblins 5 alive
!#VRv516:S0; goblins 6 alive
!#VRv517:S0; goblins 7 alive
!#VRv518:S0; goblins 8 alive

*** Gremlins ****
!?LE6/3/0;
!!FU2:P;
!!VRv511:S1; goblins 1 is dead
!!LE4/3/0:D0/1;

!?LE1/1/0;
!!FU2:P;
!!VRv512:S1; goblins 2 is dead
!!LE9/5/0:D0/1;

!?LE11/1/0;
!!FU2:P;
!!VRv513:S1; goblins 3 is dead
!!LE3/9/0:D0/1;

!?LE16/1/0;
!!FU2:P;
!!VRv514:S1; goblins 4 is dead
!!LE10/10/0:D0/1;

!?LE16/8/0;
!!FU2:P;
!!VRv515:S1; goblins 5 is dead
!!LE9/15/0:D0/1;

!?LE2/12/0;
!!FU2:P;
!!VRv516:S1; goblins 6 is dead
!!LE6/16/0:D0/1;

!?LE7/17/0;
!!FU2:P;
!!VRv517:S1; goblins 7 is dead
!!LE13/17/0:D0/1;

!?LE11/13/0;
!!FU2:P;
!!VRv518:S1; goblins 8 is dead
!!LE13/4/0:D0/1;

*** the teleporter ***
!?LE4/3/0;
!!FU1:P;
!?LE9/5/0;
!!FU1:P;
!?LE13/4/0;
!!FU1:P;
!?LE3/9/0;
!!FU1:P;
!?LE10/10/0;
!!FU1:P;
!?LE9/15/0;
!!FU1:P;
!?LE13/17/0;
!!FU1:P;
!?LE6/16/0;
!!FU1:P;

*** Find the troll ***
!?LE2/5/0;
!!VRz131:S^
Привет

Не бойся... Моя тролль-вегетарианец
Моя прости помочь ... Ыгыыыы...
Моя Псих-Мартышка... боится Гремлинов.

Псих-мартышку вези к Екатерине.

Ыгыыыы... Моя будет счастлив...
Ыыыыы %Z400

Xоди со мной к дереву.^;
!!FU16:Pz131/130/403;
!!UN:O1/7/0/1; delete troll
!!UN:O4/14/0; delete flower
!!UN:O4/15/0; delete flower
!!LE2/5/0:C0/144/1; join troll
!!VRv507:S1; Troll is with heroes

*** The tree ***
!?OB2/15/0;
!!TR1/15/0:T2/50/d/d/d/d/d/d; transform grass near tree
!!VRz200:S^
Здравствуй, %Z400, мой друг.

Я Гайа...

Пожалуйста, оставь Психомартышку на траве возле меня...

(Правым щелчком оставьте существо, после чего нажмите кнопку конца хода.)^;
!!FU16:Pz200/z110/z408; Gaпa dialogue
!!VRv509:S3;

*****************
*** The timer ***
*****************
!?TM1&v509<=2; Under the influence of gremlins
!!UN:H10/10/0/1/13; lauch timer
!!VRv509&j<=3:S1;
!!UN&v509=1:O3/4/0; delete flower
!!VRz207&v509=1:S^
Ты чувствуешь, что сила Гремлинов уменьшается.^;
!!FU16&v509=1:Pz207/z125/z400; heroes dialogue
!!VRv509&v509=1:S2; timer is off

!?TM1&v509=2; hide movement don't check the gremlins number
!!UN:H10/10/0/1/13; hide movement

!?TM1&v509=3; check for troll at the square
!!UN:H10/10/0/1/13; hide movement
!!FU6:P1/15/0/54/144; check for troll at the square
!!FU12&55:P; call FU12 if troll is on square

!?TM1&v504=3; check for troll at the square

******************************
*** Functions of Labyrinth ***
******************************
*** FUNCTION 1 : Teleport heroes
!?FU1; Teleport heroes
!!IF:Q1/21/29/1^Xи...Xи...хи-хи^;
!!VRv510:S0; init v510 to 0
!!VRv510:R7; Random 0 to 7
!!HE21&v510=0:P6/1/0;
!!HE21&v510=1:P9/13/0;
!!HE21&v510=2:P5/17/0;
!!HE21&v510=3:P14/7/0;
!!HE21&v510=4:P4/12/0;
!!HE21&v510=5:P14/1/0;
!!HE21&v510=6:P7/5/0;
!!HE21&v510=7:P14/16/0;

*** FUNCTION 2 : Found Gremlins
!?FU2; Found Gremlins
!!VRj:-1;
!!VRk:S1; init k to 1
!!VRk:R19; Random 0 to 19
!!IF:Q1/21/29/1^Ты нашел гремлинов!^;
!!HE-1:T6/3/0/29/k; provoke battle

*** FUNCTION 3 : Numbers of Gremlins
!?FU3; Numbers of Gremlins
!!VRk:S1; init k to 1
!!VRk:R19; Random 0 to 19

*** FUNCTION 4 : Disable goblin event in Labyrinth of flowers
!?FU4; Disable goblin event in Labyrinth of flowers
*kill the goblin
!!LE6/3/0&v511=0:D0/1;
!!LE1/1/0&v512=0:D0/1;
!!LE11/1/0&v513=0:D0/1;
!!LE16/1/0&v514=0:D0/1;
!!LE16/8/0&v515=0:D0/1;
!!LE2/12/0&v516=0:D0/1;
!!LE7/17/0&v517=0:D0/1;
!!LE11/13/0&v518=0:D0/1;
*kill teleporter
!!LE4/3/0:D0/1;
!!LE9/5/0:D0/1;
!!LE13/4/0:D0/1;
!!LE3/9/0:D0/1;
!!LE10/10/0:D0/1;
!!LE9/15/0:D0/1;
!!LE13/17/0:D0/1;
!!LE6/16/0:D0/1;

*** FUNCTION 5 : deactivate timer
!?FU5; deactivate timer
!!TM1:S1/1/999/0; disable timer
Pour l'instant cette fonction ne marche pas

*** FUNCTION 6 : Check for object type x4 and subtype x5 at position x1/x2/x3
!?FU6; Check for object type x4 and subtype x5 at position x1/x2/x3
!!IF:V55/0; clear flag 55
!!VRv2:Sx1;
!!VRv3:Sx2;
!!VRv4:Sx3;
!!OB2:T?y1U?y2; use x1/v3/v4 to point a square
!!IF&y1=x4/y2=x5:V55/1; set flag if Ok

*** FUNCTION 7 : Terra transformation
!?FU7; Terra transformation thanks Slava
!!DO8/0/19/1:Px16/x1;

*** FUNCTION 8 : Terra transformation
!?FU8; Terra transformation thanks Slava
!!VRv1:Sx1;
!!VRv2:Sx16;
!!VRv3:Sx2; set coordinates
!!TR1:T2/50/d/d/d/d/d/d; start rettain type

*** FUNCTION 12 : Consequence of the troll in square
!?FU12; Consequence of the troll in square
!!OB1/15/0:SHz403;
!!VRv509:S4; next state of timer (deactivate check of troll)
!!DO7/0/19/1:P0;
!!UN:S9/9/0/0/18;
!!UN:R1R2;
!!FU4:P;
!!VRz200:S^
Я чувствую себя лучше %Z400.

Я прогнала последних гремлинов с моей территории.

Люди здесь нуждаются в твоей помощи.
Слушай свое сердце...
Не забывай, что моя сила с тобой...^;
!!FU16:Pz200/z110/z408; Gaпa dialogue
!!HE21:P24/13/0;
!!VRz200:S^Прости...^;
!!FU16:Pz200/z110/z408; Gaпa dialogue
!!HE21:P26/18/0;
!!VRz200:S^Удачи...

Я буду с тобой...^;
!!FU16:Pz200/z110/z408; Gaпa dialogue