ZVSE

!#VRz199:S^..\Data\Zvs\Cmp.res^;
!#VRz198:S^B21p^;

***************
*** Prelude ***
***************
Get name dialogue
!#IF:V100/1;
Variable customization
!#VRz10:S^Время играть!^;
!#VRz11:S^Назовись^;
!#VRz12:S^Выбери себе заклинание^;
!#VRz13:S^%Z199\21_001.BMP^;
!#VRz14:S^%Z199\21_002.BMP^;
!#VRz15:S^%Z199\21_003.BMP^;
!#VRz16:S^%Z199\21_004.BMP^;
!#VRz17:S^Жажда Крови^;
!#VRz18:S^Благословение^;
!#VRz19:S^Магическая стрела^;
!#VRz20:S^Замедление^;
!#VRz21:S^Ваш герой начнет кампанию с заклинанием Жажда Крови^;
!#VRz22:S^Ваш герой начнет кампанию с заклинанием Благословения^;
!#VRz23:S^Ваш герой начнет кампанию с заклинанием Магическая Стрела^;
!#VRz24:S^Ваш герой начнет кампанию с заклинанием Замедления^;
!#IF:D1/z10/z11/z12/z13/z14/z15/z16/z17/z18/z19/z20/z17/z18/z19/z20;
!#IF:F1/z21/z22/z23/z24/0; Give hints and disable cancel button
!#IF:E100/1;
!#VRz400:Sz1; your name
!#VRz400:H10; Check if the string is empty and Flag 10
!#VRz400&-10:S^Канингем^; if the string is empty call the hero Caneghem


*********************
*** Init the game ***
*********************
*String
!#VRz2:S^^;
!#VRz11:S^^; a one name variable

! #VRz397:S^%Z199\21a.bmp^; portrait for external dialogue
!#VRz398:S^%Z198_bl.pcx^; large pcx of the heroes in short name
!#VRz399:S^%Z198_bs.pcx^; small pcx of the heroes in short name

* Variable
!#VRi:S0; I is a random valor

* Clear some variables
!#VRv504:S0;
!#VRv545:S0;
!#VRv546:S0;

* Allow heroes to live forever
!#OW:D0/254; Allow to live forever
!#OW:D1/254; Allow Nemesis to live forever

* initialisation of the timer
!#TM1:S1/999/1/1;

* No weeks, no months (thanks Slava)
!#VRz407:S^Еще одна неделя прошла.^;
!#UN:K1/1; no week of ...
!#UN:M1; no month of ...
!#UN:K2/407; set every week of message

*** Heroes and red initialization
!?GE1; First changes that cannot be done with instructions
!!HE21:A1/0/17; give magic book
!!HE16:A1/0/17; give magic book
!!HE21&v100=1:M43/1; give spell lvl 1
!!HE21&v100=2:M41/1;
!!HE21&v100=3:M15/1;
!!HE21&v100=4:M54/1;
!!HE16&v100=1:M43/1; give spell lvl 1 to the nemesis
!!HE16&v100=2:M41/1;
!!HE16&v100=3:M15/1;
!!HE16&v100=4:M54/1;
!!OW:R0/0/0; *Remove Resource
!!OW:R0/1/0;
!!OW:R0/2/0;
!!OW:R0/3/0;
!!OW:R0/4/0;
!!OW:R0/5/0;
!!OW:R0/6/0;
!!HE21:B0/z400; rename the heroes
!!HE16:B0/z400; rename the Nemesis
!!HE21:L5/z398/z399; set Nemesis' portrait
!!HE16:L5/z398/z399; set Nemesis' portrait

******************************
*** The island of Ekaterin ***
******************************
*** I N I T ***
!#VRz401:S^Дом Екатерины^;
!#OB8/29/0:SHz401; Home of Ekaterin

!#VRz402:S^Обелиск Майлза Форкосигана...
Да, неплохо...^;
!#OB13/30/0:SHz402; Obelisk of Miles Vorkosigan.

!#VRz404:S^Останки твоей лодки^;
!#OB0/31/0:SHz404; Boat

!#VRz405:S^Труп^;
!#VRz406:S^Могила^;
!#OB17/30/0:SHz405; a corpse.

!#VRz25:S^Потерянная душа^;
!#OB10/34/0:SHz25; A Lost Soul


!#VRv499:S0; wagon (o not visited)
!#VRv500:S0; ekaterin
!#VRv501:S0; tomb
!#VRv502:S0; go to labyrinth
!#VRv503:S0; in the labyrinth timer off (503=1 timer on)
!#VRv504:S0; shiprwreck
!#VRv505:S0; corpse
!#VRv506:S0; sepulture
!#VRv538:S0; lost soul

!#VRz125:S^%Z199\21_021.BMP^; samaritan (you)
!#VRz126:S^%Z199\21_022.BMP^; Ekaterin
!#VRz27:S^%Z199\21_027.BMP^; Lorenys (elf survivor)

!#VRz26:S^Екатерина^;

*** The lost soul ***
!?OB10/34/0; A Lost Soul v538
!!IF&v538=0:Q10/21/61/2^Ты видишь Потерянную Душу:

Пожалуйста, помоги мне!!
Я хочу попасть в рай, но для этого
мое тело надо похоронить.

Я хочу обрести покой...
ПОМОГИ!...

Ты согласен помочь потерянной душе?^;
!!IF&10/v538=0:Q10/21/61/1^Спасибо...
Пусть Гайа благословит тебя^;
!!IF&-10/v538=0:Q10/21/61/1^Пожалуйста,
ты моя последняя надежда...^;
!!VRv538:S1;
!!IF&v538=1:Q10/21/61/1^Я жду^;

!?LE10/34/0;
!!IF&10:M^За спасение потерянной души

ты получил:^;
!!LE10/34/0&10:E1000;

*** the shipwreck ***
!?LE2/31/0;
!!VRz12:S^Лоренис^;
!!IF&v505=1:M^Ты пришел слишком поздно
уже ничего не исправить...^;
!!VRv504&v505=1:S1;
!!IF&v504=0:M^Ты пытаешься найти выживших и хоть какие-то припасы.^;
!!IF&v504=0:Q1/8/7/1^Ты нашел топор.^;
!!HE-1&v504=0:A1/7/3;

!!IF&v504=0:Q1^Ты уже был готов покинуть останки лодки, когда услышал приглушенные голоса.
они шли изнутри корабля...

Твой экипаж в ловушке там, внутри!

Бросишь ли ты вызов скалам, чтобы спасти их?^;

!!VRv545&v504=0/1:S1;
!!VRv546&v504=0:S1;

!!IF&v504=0/1:M^Ты презрел страх и отправился спасать их. Вода холодная, и разобрать обломки кораблекрушения оказалось непросто. Но ты все равно это сделал, потому что заботишься о своем экипаже!^;

!!IF&v504=0/-1:M^Волны и скалы выглядят слишком страшно, чтобы ты решился плыть туда. В конце концов спасти собственную жизнь важнее!

А они пусть выбираются как могут...^;

!!FU16&v504=0/v545=0/v546=0:Pz11/27/12;
!!LE2/31/0&v504=0/1:C0/19/10;
!!IF&v504=0/1:Q1/17/1000/1^Ты получил 1000 очков опыта за спасение Лоренса и его Эльфов^;
!!HE-1&v504=0/1:Ed+1000; Give 1000 experience
!!VRv504&v504=0:S1;
!!VRv505&v504=0:S1;


*** the ship disappears ***
!?LE5/31/0;
!!VRz200:S^Ты услышал ужасный звук.

Это останки корабля грохотали в волнах...

Ты чувствуешь себя значительно лучше, найдя Лоренса и его эльфов.^;
!!VRz200&v504=0/v545=0:S^Ты услышал ужасный звук.

Это останки корабля грохотали в волнах...

А ты так и не искал выживших...

Стыдись.^;
!!VRz200&v504=1/v545=0:S^Ты услышал ужасный звук.

Это останки корабля грохотали в волнах...

А ты даже не попытался спасти своих друзей.

Стыдись!^;
!!FU16:Pz200/z125/z400; heroes dialogue
!!UN:O1/31/0; Delete shipwreck
!!UN:I1/32/0/29/0; Place Flotsam
!!UN:I0/31/0/29/0; Place flotsam
!!UN:I1/33/0/59/0; Place bottle
!!VRv505:S1;
!!IF:V4/0;

*** the sepulture ***
!?OB17/30/0;
!!VRi:S0;
!!IF:V2/0;
!!OW:R0/2/?i;
!!IF&v506=1:M^Ты много работал, чтобы построить эту гробницу, и гордишься проделанной работой.^;
!!IF&v506<>1:Q2^
Ты видишь труп.
Это тебя раздражает.
Xочешь построитьдля него гробницу?

Для этого тебе будет нужно
5 единиц руды.^;
!!FU10&2/i>4:P;
!!IF&2/i<5/v506<>1:Q1/2/5/1^Тебе нужно 5 единиц руды^;

*** The Wagon ***
!?OB4/30/0;
!!WG4/30/0&v499=1:S0;
!!WG4/30/0&v499=0:S1B0R2/5; Wagon 5 ore
!!VRv499&v499=0:S1;

*** Ekaterin *** z131/127/26
!?OB8/29/0;
!!VRz131&v500=0:S^%Z400

Ты видишь прекрасную женщину.
Ее взгляд очень теплый и дружелюбный.

Ты понимаешь, что ни в чем не сможешь ей отказать.^;
!!FU16&v500=0:Pz131/126/26;
!!VRz131&v500=1:S^Пожалуйста %Z400
ты обещал почистить обелиск.^;
!!VRz131&v500>=2:S^Спасибо, %Z400.^;
!!FU16&v500>=1:Pz131/126/26;
!!UN&v500=2:I8/30/0/26/0; place the event for the rewards
!!VRv500&v500=2:S3;
!!IF&v500=0:Q1^Привет,

у меня есть для тебя работа.
Ты же не откажешься почистить обелиск моего мужа?^;
!!IF&v500=0/1:M^Xорошо. Вот тебе тряпки и моющее средство.^;
!!VRv500&1:S1; put 500=1 (accept to clean
!!VRv500&v501=1:S3; a protect for cleaning only one time

!!VRv502&v501=1:S1; go to labyrinth
!!IF&-1:M^Остров, похоже, небольшой.
Может быть, она поможет тебе с него выбраться.^;
!!OB8/29/0&v500>=2:D0;

The event for experience
!?LE8/30/0;
!!LE8/30/0:L1;
!!IF:Q1/17/500/1^Ты получил 500 очков опыта
за мытье обелиска.^;
!!HE-1:Ed+500; Give 500 experience


*** the obelisk ***
!?OB13/30/0;
!!IF&v500<=1:M^Прекрасный обелиск.
Только очень грязный.^;
!!IF&v500>=2:M^Можешь гордиться.
Обелиск выглядит гораздо лучше...^;
!!HE-1&v500=1:P13/31/0/1; move the heroes
!!UN&v500=1:O13/30/0; delete the dark obleisk
!!UN&v500=1:I13/30/0/57/0/57/0/3; place the obelisk
!!IF&v500=1:M^Xорошо потрудился.
Стало гораздо чище!^;
!!VRv500&v500=1:S2; obelisk clean
!!VRv501&v500=2:S1; with V501=1 the obelisk can't be cleaned a 2nd time

*** Go to labyrinth ***
!?LE19/32/0; the players helps grand ma
!!VRz131&v502=1/v538=3:S^Ты слышишь шепот прямо в мозгу.

Это голос Екатерины:

"Помоги мне найти мою зверушку.
Она потерялась в Лабиринте Цветов ..."

Ты не можешь отказаться, потому что только она может
помочь тебе выбраться с острова.^;
!!FU16&v502=1/v538=3:Pz131/126/26;
!!HE-1&v502=1/v538=3:P16/17/0; move the heroes
!!IF&v502=1/v538=3:M^Тебе надо найти зверушку Екатерины.
И это ужасно, потому что ты даже не знаешь, как она выглядит.

Вдруг ты слышишь чей-то противный смех:

Это Гремлины.

Они будут играть с тобой. Шутить над тобой.
Тебе придется поймать их всех, или ты никогда не найдешь того, кого ищешь.
И это будет нелегко!

Ррррр...^;

*** FUNCTION 10: Delete sepulture
!?FU10; Delete sepulture
!!UN:O17/30/0; delete
!!UN:I17/30/0/108/0/108/0/1; place the tomb
!!VRi:-5; Substract -5 to i (the cost in ore)
!!OW:R0/2/i; Substract 5 ore (i)
!!UN:R1R2; Redraw screen and ressource
!!VRv506:S1; Close the event
!!OB17/30/0:Hz406; Rename the object
!!IF:Q10/21/61/1^Спасибо, что похоронил меня...
Теперь я отправляюсь на Небеса...
Пусть Гайа благословит тебя...^;
!!IF:Q1/17/500/1^Ты получаешь 500 очков опыта
за спасение потерянной души^;
!!HE21:Ed+500; Give 500 experience
!!VRv538:S3;
!!UN:O10/34/0/1; delete wraith and hint
!!UN:I10/34/0/26/0; place an event
!!LE10/34/0:L1;
