ZVSE

!#UN:P5/0;
*** Venus's Blessing **** Increases morale and luck (w12)
!#VRz18:S^Благословление Венеры (Не посещено)^;
!#VRz19:S^Благословление Венеры (Посещено)^;
!#HT63/2:W12/0/18;
!#HT63/2:W12/1/19;
!#HT63/2:W12/2/19;
!#HT63/2:W12/3/19;

!?OB63/2;
!!IF:W-1; Call w variable for current hero
!!FU816:P-1/1;
!!VRw12:S3; fill potion  with 3 doses
!!IF&1000:Q1/11/1/14/1/1^{Благословление} {Венеры}

%Z1 подходит к магнетической статуе...

%Z1 наполняет фляжку водой из магического фонтана. Теперь %Z3 имеет три склянки {Благословления} {Венеры}.

Это снадобье повышает мораль и удачу во время боя.^;

*** Hydromel Fountain **** Increases Attack and Defense (w10)
* (Fountain of Fortune)
!?OB30/0;
!!UN:P17/?y2; Check if Potion Fountains script is enabled in WoGify Options.
* Continue trigger if script is enabled.
!!VRy2:S1;  Ignore WoGify status, and activate the script.
!!IF&y2=1:W-1; Call w variable for current hero
!!FU816&y2=1:P-1/1;
!!VRw10&y2=1:S3; fill potion  with 3 doses
!!IF&1000/y2=1:Q1/31/2/32/2/1^{Фонтан} {Удачи}

%Z1 наполняет фляжку водой из магического фонтана.
Теперь %Z3 имеет три склянки {Фонтан} {Удачи}.

Это снадобье повышает Атаку и Защиту во время боя.^;

*** Berserker Potion *** (w11)
!#VRz20:S^Фонтан Валхаллы (Не посещено)^;
!#VRz21:S^Фонтан Валхаллы (Посещено)^;
!#HT63/48:W11/0/20;
!#HT63/48:W11/1/21;
!#HT63/48:W11/2/21;
!#HT63/48:W11/3/21;

!?OB63/48;
!!IF:W-1; Call w variable for current hero
!!FU816:P-1/1;
!!VRw11:S3; fill potion with 3 doses
!!IF&1000:Q1/31/10/11/3/35/0/1^{Фонтан} {Валхаллы}

%Z1 наполняет фляжку водой из магического фонтана.
Теперь %Z3 имеет три склянки {Снадобья} {Берсерка}.

Это снадобье повышает Атаку и удачу во время боя, но не дает Герою возможности творить заклинания.^;

**** U S E    P O T I O N S   I N   B A T T L E ***
*** At the beginning of battle ***
!?BA0;
!!IF:V902/0 V903/0; init to Hydromel potion
!!IF:V904/0 V905/0; init to Berserker potion
!!IF:V906/0 V907/0; init to Venus potion
!!BA:E?v1; check for MP battle
!!FU800&v1<2:P; non AI against Human battle

!?FU800; At the beginning of battle (init)
!!VRy3:S0;
!!VRy4:S0;
!!VRy5:S0;
!!VRy6:S1; check by default that defender is managed by AI
!!BA:H0/?v1260; Identity of attacker and store in v1260
!!BA:H1/?v1261; Identity of defender and store in v1261
!!HEv1260:O?y3; owner of attacker
!!HEv1261&v1261>-1:O?y4; owner of defender
!!VRy4&v1261<0:S99; ho hero at all
!!OW:Iy3/?y5; check if Attacker is human (AI=1)
!!OW&v1261>-1:Iy4/?y6; check if defender is human (AI=1)
!!VRy20:S0; [assume we don't have two human players]
!!VRy20&v1261>-1/y5=0/y6=0:S1; [set flag to mean both players are human]

*** Hydromel Potion
!!IF:Wv1260; check if attacker has Hydromel potion
!!VRy7&w10>=1:Sw10; attacker has Hydromel potion
!!IF&v1261>-1:Wv1261; check if defender has Hydromel potion
!!VRy8&w10>=1/v1261>-1:Sw10; defender has Hydromel potion
!!FU801&y7>=1:Pv1260/y5/y3; call fu801 if att. has Hydromel potion
!!FU802&y8>=1/y20=0:Pv1261/y6/y4; call fu802 if def. has Hydromel potion, except human vs. human
*** Berserker Potion
!!IF:Wv1260; ckeck if attacker has Berserker Potion
!!VRy9&w11>=1:Sw11; attacker has Berserker Potion
!!IF&v1261>-1:Wv1261; ckeck if defender has Berserker Potion
!!VRy10&w11>=1/v1261>-1:Sw11; defender has Berserker Potion
!!FU810&y9>=1:Pv1260/y5/y3; call fu810 if attacker has Berserker Potion
!!FU811&y10>=1/y20=0:Pv1261/y6/y4; call fu811 if defender has Berserker Potion, except human vs. human
*** Venus Potion
!!IF:Wv1260; ckeck if attacker has Venus potion
!!VRy11&w12>=1:Sw12; attacker has Venus potion
!!IF&v1261>-1:Wv1261; ckeck if defender has Venus potion
!!VRy12&w12>=1/v1261>-1:Sw12; defender has Venus potion
!!FU814&y11>=1:Pv1260/y5/y3; call fu814 if att. has Venus potion
!!FU815&y12>=1/y20=0:Pv1261/y6/y4; call fu815 if def. has Venus potion, except human vs. human

!?FU801; Attacker has Hydromel potion
!!IF:Wx1; !!FU816:Px1/w10;
!!IF&x2=0/1000:Q1/31/2/32/2/2^{Снадобье} {Водяного}

(%W10 %Z4 осталось)

Если %Z1 выпьет Снадобье Водяного, %Z3 получит +2 Атаки и +2 Защиты на время этого боя...

%Z1, вы хотите выпить Снадобье Водяного?

(Если вы выпьете {Снадобье} {Водяного} и {Снадобье} {Берсерка} вместе,
 есть шанс потерять 1 очко Силы после боя, поэтому будьте осторожны с этим)
^;
*** Human
!!IF&1:V902/1;
!!VRw10&x2=0/902:-1; remove a dose
!!HEx1&x2=0/902:Fd2/d2/d/d; give bonus
*** AI
!!IF&x2=1:V902/1;
!!VRw10&x2=1:-1; remove a dose
!!HEx1&x2=1:Fd2/d2/d/d; boost

!?FU802; Defender has Hydromel potion
!!IF:Wx1; !!FU816:Px1/w10;
!!IF&x2=0/1000:Q1/31/2/32/2/2^{Снадобье} {Водяного}

(%W10 %Z4 осталось)

Если %Z1 выпьет Снадобье Водяного, %Z3 получит +2 Атаки и +2 Защиты на время этого боя...

%Z1, вы хотите выпить Снадобье Водяного?

(Если вы выпьете {Снадобье} {Водяного} и {Снадобье} {Берсерка} вместе,
 есть шанс потерять 1 очко Силы после боя, поэтому будьте осторожны с этим)
^;
!!IF&1:V903/1;
!!VRw10&x2=0/903:-1; remove a dose
!!HEx1&x2=0/903:Fd2/d2/d/d; give bonus
*** Defender AI
!!IF&x2=1:V903/1;
!!VRw10&x2=1:-1; remove a dose
!!HEx1&x2=1:Fd2/d2/d/d; boost

!?FU810; Attacker has Berserker potion
!!IF:Wx1; !!FU816:Px1/w11;
!!IF&x2=0/1000:Q1/31/10/11/3/35/0/2^{Снадобье} {Берсерка}

(%W11 %Z4 осталось)

Если %Z1 выпьет Снадобье Берсерка, %Z3 получит +10 Атаки и максимум удачи на время этого боя... но не сможет творить заклинания.

%Z1, вы хотите выпить Снадобье Берсерка?

(Если вы выпьете {Снадобье} {Водяного} и {Снадобье} {Берсерка} вместе,
 есть шанс потерять 1 очко Силы после боя, поэтому будьте осторожны с этим)
^;
!!IF&1:V904/1;
!!VRw11&x2=0/904:-1; remove a dose
!!HEx1&x2=0/904:Fd10/d/d/d;  give bonus
!!HEx1&x2=0/904:R1/3; boost luck
!!HEx1&x2=0/904:I?v40; store spell points of attacker in v40
!!HEx1&x2=0/904:I0; put spell points to 0
*** attacker AI - don't drink
!!IF&x2=1:V904/0; AI don't drink

!?FU811; Defender has Berserker potion
!!IF:Wx1; !!FU816:Px1/w11;
!!IF&x2=0/1000:Q1/31/10/11/3/35/0/2^{Снадобье} {Берсерка}

(%W11 %Z4 осталось)

Если %Z1 выпьет Снадобье Берсерка, %Z3 получит +10 Атаки и максимум удачи на время этого боя... но не сможет творить заклинания.

%Z1, вы хотите выпить Снадобье Берсерка?

(Если вы выпьете {Снадобье} {Водяного} и {Снадобье} {Берсерка} вместе,
 есть шанс потерять 1 очко Силы после боя, поэтому будьте осторожны с этим)
^;
!!IF&1:V905/1;
!!VRw11&x2=0/905:-1; remove a dose
!!HEx1&x2=0/905:Fd10/d/d/d; give bonus
!!HEx1&x2=0/905:R1/3; boost luck
!!HEx1&x2=0/905:I?v41; store spell points of attacker in v41
!!HEx1&x2=0/905:I0; put spell points to 0
*** Defender AI
!!IF&x2=1:V905/0; AI don't drink

!?FU814; Attacker has Venus potion
!!IF:Wx1; !!FU816:Px1/w12;
!!IF&x2=0/1000:Q1/11/1/14/1/2^{Снадобье} {Благословления} {Венеры}

(%W12 %Z4 осталось)

Если %Z1 выпьет Снадобье Благословления Венеры, %Z3 будет иметь максимум морали и удачи на время этого боя.

%Z1, вы хотите выпить Снадобье Благославление Венеры?^;
!!IF&1:V906/1;
!!VRw12&x2=0/906:-1; remove a dose
!!HEx1&x2=0/906:R1/3 R0/3; give bonus
*** attacker AI
!!VRw12&x2=1/x2=1:-1; remove a dose
!!HEx1&x2=1/x2=1:R1/3 R0/3; boost
!!IF&x2=1/x2=1:V906/1;

!?FU815; Defender has Venus potion
!!IF:Wx1; !!FU816:Px1/w12;
!!IF&x2=0/1000:Q1/11/1/14/1/2^{Снадобье} {Благословления} {Венеры}

(%W12 %Z4 осталось)

Если %Z1 выпьет Снадобье Благославление Венеры, %Z3 будет иметь максимум морали и удачи на время этого боя.

%Z1, вы хотите выпить Снадобье Благославление Венеры?^;
!!IF&1:V907/1;
!!VRw12&x2=0/907:-1; remove a dose
!!HEx1&x2=0/907:R1/3 R0/3; give bonus
*** Defender AI
!!IF&x2=1:V907/0; AI Don't drink

*****************************
*** At the end of battle  ***
*****************************
!?BA1;
!!VRv3:S0;
!!VRv4:S0;
!!VRv5:S0;
!!VRv6:S1; check by default that defender is managed by AI
!!HEv1260:O?v3; owner of attacker
!!HEv1261&v1261>-1:O?v4; owner of defender
!!VRv4&v1261<0:S99;
!!OW:Iv3/?v5; check if Attacker is human (AI=1)
!!OW&v1261>-1:Iv4/?v6; check if defender is human (AI=1)
*** Hydromel Potion
!!HEv1260&902:Fd-2/d-2/d/d; Remove bonus if attacker has drunk
!!HEv1261&903:Fd-2/d-2/d/d; Remove bonus if defender has drunk
*** Berserker Potion
* attacker CF4
!!HEv1260&904:Fd-10/d/d/d; Remove bonus if attacker has drunk
!!HEv1260&904:Idv40; Reset Spell point
!!VRv1:S0T100;
!!FU812&902/904/v1>50:Pv1260/v5; att. has drunk 2 potions
* defender CF5
!!HEv1261&905:Fd-10/d/d/d; Remove bonus if defender has drunk
!!HEv1261&905:Idv41; Reset Spell points
!!VRv1:S0T100;
!!FU812&903/905/v1>50:Pv1261/v6; def. has drunk 2 potions

*** hero has drunk 2 potions
!?FU812; Player loses Spell power if hero drank 2 potions
!!HEx1:Fd/d/?y1/d/1; Return "clear" knowledge (without artifacts)
!!HEx1&y1>1:Fd/d/d-1/d;
!!FU816:Px1/1;
!!IF&y1>1/x2=0:Q1/33/0/1^{Снадобье} {Берсерка}

%Z1 снадобья Водяного и Берсерка смешались, и вступили в плохую реакцию... %Z1 навсегда теряет 1 очко Силы.^;

!?FU816; ZVS hero depending string preparation
*** x1=hero index
*** x2=doses
!!HEx1:B0/?z1; Store name in z1
!!HEx1:R2/?y1; Check sex
!!VRz2&y1=0:S^его^; Set "his" or "her" depending on sex
!!VRz2&y1=1:S^ее^;
!!VRz3&y1=0:S^он^; Set "he" or "she" depending on sex
!!VRz3&y1=1:S^она^;
!!VRz4&x2=1:S^склянка^;
!!VRz4&x2>1:S^склянки^;