ZVSE

** on saturday (6th day of a week set var to 1)
!#TM15:S6/999/7/255;
!?TM15;
!!VRv152:S1;

** on sunday (last day of a week set var to 0)
!#TM16:S7/999/7/255;
!?TM16;
!!VRv152:S0;
!!VRv155:S0; ** Set Seller after visiting

** on wednesday (3rd day of a week set var to 1)
!#TM17:S3/999/7/255;
!?TM17;
!!VRv152:S1;

** on thursday (4th day of a week set var to 0)
!#TM18:S4/999/7/255;
!?TM18;
!!VRv152:S0;
!!VRv155:S0; ** Set Seller after visiting

!#VRz151:S^Барахолка^;
!#VRv151:S0;
!#HT63/7:V151/0/151;

!?OB63/7;
!!OW:R-1/6/?v160; ** Check Money of visiting player

!!IF&1000/v152=0:M^Барахолка открыта только по четвергам и субботам.^;
!!IF&1000/v152=1/v160<1000/v155<3:M^У вас недостаточно денег для торговли с продавцами. Они игнорируют вас!^;
!!IF&1000/v152=1/v155>2:M^Торговцы уже расходятся. Вряд ли они продадут вам что-либо^;
!!FU153&v152=1/v160>1000/v155<3:P;

!?FU153;
!!VRv154:S0 R6; ** just the offer pic

!!IF&1000:Q156/v154/1/2^Торговец предлагает вам нечто, что выглядит как ресурс, но возможно, это что-то ценное^;

!!IF&1000/-156:M^Возвращайтесь позднее!^;

!!VRv157:S0 R6; ** Random Var

!!VRy11:S7; ** Random Atifakt
!!VRy10:S0 R 78;
!!VRy11:+y10;
!!VRy11&y11=72:+1;
!!VRy12:S0 R9; **Pay Price Var
!!VRy12:+1;
!!VRy12:*100;
!!VRy13:Sv154; ** Get a random Resource Bless
!!VRy13:+15;
!!VRy14:S0 R3; ** Random Primary Skill
!!VRy14:+31;
!!VRy15:S0 R46;** Random Spellscroll until Titans Lightning Bold
!!VRy16:Sy15;
!!VRy15:+1010;
!!VRy16:+9;


** Get nothing Seller steals 1000 gold
!!IF&156/v157=0:Q158/6/-1000/1^Вы даже не заметили. как из вашего кармана пропало золото^;
!!OW&156/v157=0:R-1/6/d-1000;

** Pay if the seller doesn`t steal
!!IF&156/v157>0:Q158/6/y12/1^Вы покупаете товар^;

** Get an random Artifakt
!!IF&156/v157=1:Q158/8/y11/1^Вы осматривате, что вы купили, и замечаете, что это больше чем просто ресурс^;
!!HE-1&156/v157=1:Ay11;
!!VRv160&156/v157=1:-y12;
!!OW&156/v157=1:R-1/6/v160;

** Get what you saw - 1 random resource
!!IF&156/v157=2:Q158/v154/1/1^Оказалось, что ваше зрение вас не обмануло...^;
!!OW&156/v157=2:R-1/v154/d1;
!!VRv160&156/v157=2:-y12;
!!OW&156/v157=2:R-1/6/v160;

** Get a random Resoucre Bless^;
!!IF&156/v157=3:Q158/v154/1/1^Вы осматриваете покупку, и вдруг понимаете, что знаете, как можно получать больше этих ресурсов!^;
!!HE-1&156/v157=3:Yy13/1/7/1;
!!VRv160&156/v157=3:-y12;
!!OW&156/v157=3:R-1/6/v160;

** Get a random primary skill
!!IF&156/v157=4:Q158/y14/1/1^Вы осматриваете покупку, и чувствуете, что в вас заползают новые знания.^;
!!HE-1&156/v157=4/y14=31:Fd1/d/d/d;
!!HE-1&156/v157=4/y14=32:Fd/d1/d/d;
!!HE-1&156/v157=4/y14=33:Fd/d/d1/d;
!!HE-1&156/v157=4/y14=34:Fd/d/d/d1;
!!VRv160&156/v157=4:-y12;
!!OW&156/v157=4:R-1/6/v160;

** Get a random Spellscroll
!!IF&156/v157=5:Q158/9/y16/1^Вы осматриваете покупку, и вдруг она превращается в свиток с заклинанием.^;
!!HE-1&156/v157=5:Ay15;
!!VRv160&156/v157=5:-y12;
!!OW&156/v157=5:R-1/6/v160;

** Get a Stone ;)
!!IF&156/v157=6:M^Это оказался простой камень.^;
!!VRv160&156/v157=6:-y12;
!!OW&156/v157=6:R-1/6/v160;

!!VRv155&156:+1;