ZVSE

** [Flea Market]
** First Experience Script 1.1 - Dieter Averbeck
** This Script opens a flea market on every Saturday and Thursday,
** the first visiting heroe can deal with the market man.
** The Market Man offers a resource, if you have enough money
** You don`t get a resource every time most something better,
** sometimes something worse.
** To use this script, just copy and paste it into a timed
** event, that not will be shown.
** For example Day 500. Then place one or more of the object 6
** on the map (It`s the 5th building after the towns). That`s it!
** Variables used: TM15,TM16,TM17,TM18, z151, v151-y157,
** FU153, Flag156, 158

** on saturday (6th day of a week set var to 1)
!#TM15:S6/999/7/255;
!?TM15;
!!VRv152:S1;

** on sunday (last day of a week set var to 0)
!#TM16:S7/999/7/255;
!?TM16;
!!VRv152:S0;
!!VRv155:S0; ** Set Seller after visiting

** on wednesday (3rd day of a week set var to 1)
!#TM17:S3/999/7/255;
!?TM17;
!!VRv152:S1;

** on thursday (4th day of a week set var to 0)
!#TM18:S4/999/7/255;
!?TM18;
!!VRv152:S0;
!!VRv155:S0; ** Set Seller after visiting

!#VRz151:S^Барахолка^;
!#VRv151:S0;
!#HT63/7:V151/0/151;

!?OB63/7;
!!OW:R-1/6/?v160; ** Check Money of visiting player

!!IF&1000/v152=0:M^Барахолка открыта только по четвергам и субботам^;
!!IF&1000/v152=1/v160<1000/v155<3:M^У вас недостаточно денег для торговли. Продавцы игнорируют вас!^;
!!IF&1000/v152=1/v155>2:M^У вас нет ничего, что могло бы их заинтересовать. Попробуйте снова на следующей неделе^;
!!FU153&v152=1/v160>1000/v155<3:P;

!?FU153;
!!VRv154:S0 R6; ** just the offer pic

!!IF&1000:Q156/v154/1/2^Продавец предлагает вам нечто, похожее на ресурс, но ,возможно, это что-то более ценное^;

!!IF&1000/-156:M^Возвращайтесь позже^;

!!VRv157:S0 R6; ** Random Var

!!VRy11:S7; ** Random Atifakt
!!VRy10:S0 R 78;
!!VRy11:+y10;
!!VRy11&y11=72:+1;
!!VRy12:S0 R9; **Pay Price Var
!!VRy12:+1;
!!VRy12:*100;
!!VRy13:Sv154; ** Get a random Resource Bless
!!VRy13:+15;
!!VRy14:S0 R3; ** Random Primary Skill
!!VRy14:+31;
!!VRy15:S0 R46;** Random Spellscroll until Titans Lightning Bold
!!VRy16:Sy15;
!!VRy15:+1010;
!!VRy16:+9;


** Get nothing Seller steals 1000 gold
!!IF&156/v157=0:Q158/6/-1000/1^Вы даже не заметили, как из кармана пропало золото!^;
!!OW&156/v157=0:R-1/6/d-1000;

** Pay if the seller doesn`t steal
!!IF&156/v157>0:Q158/6/y12/1^Вы платите цену продавца^;

** Get an random Artifakt
!!IF&156/v157=1:Q158/8/y11/1^Вы осматриваете покупку и замечаете, что это больше, чем кусок ресурса!^;
!!HE-1&156/v157=1:Ay11;
!!VRv160&156/v157=1:-y12;
!!OW&156/v157=1:R-1/6/v160;

** Get what you saw - 1 random resource
!!IF&156/v157=2:Q158/v154/1/1^Вы получили просто то, что вы и покупали^;
!!OW&156/v157=2:R-1/v154/d1;
!!VRv160&156/v157=2:-y12;
!!OW&156/v157=2:R-1/6/v160;

** Get a random Resoucre Bless^;
!!IF&156/v157=3:Q158/v154/1/1^Вы осматриваете покупку, и вдруг чувствуете, что в вашем сознание появился способ, как увеличить количество этих ресурсов.^;
!!HE-1&156/v157=3:Yy13/1/7/1;
!!VRv160&156/v157=3:-y12;
!!OW&156/v157=3:R-1/6/v160;

** Get a random primary skill
!!IF&156/v157=4:Q158/y14/1/1^Вы осматриваете покупку, и чувствуете, что что-то происходит.^;
!!HE-1&156/v157=4/y14=31:Fd1/d/d/d;
!!HE-1&156/v157=4/y14=32:Fd/d1/d/d;
!!HE-1&156/v157=4/y14=33:Fd/d/d1/d;
!!HE-1&156/v157=4/y14=34:Fd/d/d/d1;
!!VRv160&156/v157=4:-y12;
!!OW&156/v157=4:R-1/6/v160;

** Get a random Spellscroll
!!IF&156/v157=5:Q158/9/y16/1^Вы осматриваете покупку, неожиданно она изменяется, и у вас в руках свиток заклинания!^;
!!HE-1&156/v157=5:Ay15;
!!VRv160&156/v157=5:-y12;
!!OW&156/v157=5:R-1/6/v160;

** Get a Stone ;)
!!IF&156/v157=6:M^Это просто камень!^;
!!VRv160&156/v157=6:-y12;
!!OW&156/v157=6:R-1/6/v160;

!!VRv155&156:+1;